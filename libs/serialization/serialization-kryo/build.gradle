import aQute.bnd.gradle.Bundle

plugins {
    id 'corda.common-library'
    id 'corda.osgi-test-conventions'
    id 'com.r3.internal.gradle.plugins.r3Publish'
}

description 'Corda Kryo Serialization Library'

configurations {
    cpks {
        canBeConsumed = false
        transitive = false
    }
}

dependencies {
    compileOnly 'org.osgi:osgi.annotation'
    compileOnly 'org.osgi:osgi.core'
    compileOnly 'org.osgi:org.osgi.service.component.annotations'
    compileOnly "biz.aQute.bnd:biz.aQute.bnd.annotation:$bndVersion"

    implementation platform("net.corda:corda-api:$cordaApiVersion")
    implementation "net.corda:corda-application"
    implementation "net.corda:corda-base"
    implementation "net.corda:corda-packaging"
    implementation "net.corda:corda-serialization"

    implementation "co.paralleluniverse:quasar-core-osgi:$quasarVersion"
    implementation "com.esotericsoftware:kryo:$kryoVersion"
    implementation project(path: ':libs:serialization:kryo-serializers', configuration: 'bundle')
    implementation 'net.corda.kotlin:kotlin-stdlib-jdk8-osgi'
    implementation "org.bouncycastle:bcprov-jdk15on:$bouncyCastleVersion"
    implementation "org.bouncycastle:bcpkix-jdk15on:$bouncyCastleVersion"
    implementation "org.slf4j:slf4j-api"

    implementation project(":libs:crypto:crypto-internal")
    implementation project(":libs:sandbox")
    implementation project(":libs:serialization:checkpoint-serialization-api")
    implementation project(path: ':libs:serialization:serialization-encoding', configuration: 'bundle')
    implementation project(":libs:utilities")

    cpks project(path: 'src:integrationTest:cpks:serializable-cpk-one', configuration: 'cordaCPK')
    cpks project(path: 'src:integrationTest:cpks:serializable-cpk-two', configuration: 'cordaCPK')

    testCompileOnly 'org.osgi:osgi.core'
    testImplementation "org.assertj:assertj-core:$assertjVersion"
    testImplementation "org.junit.jupiter:junit-jupiter-api:$junit5Version"
    testImplementation "org.mockito.kotlin:mockito-kotlin:$mockitoKotlinVersion"
    testImplementation "net.i2p.crypto:eddsa:$eddsaVersion"
    testRuntimeOnly "org.apache.felix:org.apache.felix.framework:$felixVersion"
    testRuntimeOnly "co.paralleluniverse:quasar-core-osgi:$quasarVersion:agent"

    integrationTestImplementation project(":components:install:install-legacy")
    integrationTestImplementation project("src:integrationTest:cpks:serializable-cpk-one")
    integrationTestImplementation "org.osgi:org.osgi.util.promise:$osgiUtilPromiseVersion"
    integrationTestCompileOnly "org.osgi:org.osgi.service.cm:$osgiCmVersion"
    integrationTestCompileOnly "org.osgi:org.osgi.service.component:$osgiServiceComponentVersion"
    integrationTestRuntimeOnly "org.apache.felix:org.apache.felix.framework:$felixVersion"
    integrationTestRuntimeOnly "org.apache.felix:org.apache.felix.configadmin:$felixConfigAdminVersion"
    integrationTestRuntimeOnly "co.paralleluniverse:quasar-core-osgi:$quasarVersion:agent"

    integrationTestImplementation project(":libs:crypto:crypto-impl")
    integrationTestImplementation project(":libs:messaging:inmemory-messaging-impl")
    integrationTestImplementation project(":libs:lifecycle:lifecycle-impl")
    integrationTestRuntimeOnly project(":libs:sandbox-hooks")
    integrationTestRuntimeOnly project(":libs:sandbox-internal")
}

def integrationTestResources = tasks.named('processIntegrationTestResources', ProcessResources) {
    from configurations.cpks
}

tasks.named('testingBundle', Bundle) {
    dependsOn integrationTestResources
}
