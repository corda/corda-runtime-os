import aQute.bnd.gradle.Bundle

plugins {
    id 'corda.common-library'
    id 'corda.osgi-test-conventions'
    id 'corda.common-publishing'
    id 'org.jetbrains.kotlin.jvm'
}

description 'Corda Sandbox Internals'

configurations {
    cpbs {
        canBeConsumed = false
        transitive = false
    }
}

dependencies {
    compileOnly "org.osgi:org.osgi.service.component:$osgiServiceComponentVersion"
    compileOnly 'org.osgi:org.osgi.service.component.annotations'
    compileOnly "org.osgi:osgi.annotation"
    compileOnly "org.osgi:osgi.core"
    api 'net.corda.kotlin:kotlin-stdlib-jdk8-osgi'
    implementation platform("net.corda:corda-api:$cordaApiVersion")
    implementation 'net.corda:corda-packaging'
    implementation 'net.corda:corda-serialization'
    implementation project(':libs:sandbox')
    runtimeOnly project(':libs:sandbox-hooks')

    testCompileOnly 'org.osgi:osgi.core'
    testCompileOnly "org.osgi:org.osgi.service.component:$osgiServiceComponentVersion"
    testImplementation "org.mockito.kotlin:mockito-kotlin:$mockitoKotlinVersion"
    testRuntimeOnly "org.apache.felix:org.apache.felix.framework:$felixVersion"
    testRuntimeOnly "org.apache.felix:org.apache.felix.scr:$felixScrVersion"
    testRuntimeOnly "org.osgi:org.osgi.util.promise:$osgiUtilPromiseVersion"

    cpbs project(path: 'sandbox-cpk-one', configuration: 'cordaCPB')
    cpbs project(path: 'sandbox-cpk-three', configuration: 'cordaCPB')
    cpbs project(path: 'sandbox-irresolvable-cpk', configuration: 'cordaCPB')

    integrationTestImplementation project(':testing:sandboxes')
    integrationTestImplementation "net.corda:corda-application"
    integrationTestRuntimeOnly project(':libs:crypto:crypto-impl')
    integrationTestRuntimeOnly project(':libs:lifecycle:lifecycle-impl')
    integrationTestRuntimeOnly project(':libs:messaging:messaging-impl')
    integrationTestRuntimeOnly project(':libs:messaging:db-message-bus-impl')
}

def integrationTestResources = tasks.named('processIntegrationTestResources', ProcessResources) {
    from(configurations.cpbs) {
        into 'META-INF'
        rename "(.*)-\\Q${version}\\E-package.cpb", "\$1.cpb"
    }
}

tasks.named('testingBundle', Bundle) {
    dependsOn integrationTestResources
}
