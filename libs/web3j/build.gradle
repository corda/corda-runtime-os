import static aQute.bnd.version.MavenVersion.parseMavenString

plugins {
    id 'java-library'
    id 'biz.aQute.bnd.builder'
    id 'com.jfrog.artifactory'
    id 'maven-publish'
}

description 'Web3j'

configurations {
    bundle {
        canBeDeclared = false
        canBeResolved = false
    }
}

dependencies {
    compileOnly "org.web3j:core:$web3jVersion"
}

tasks.named('jar', Jar) {
    archiveBaseName = 'corda-web3j'

    ext {
        bundleVersion = parseMavenString(web3jVersion).OSGiVersion
    }

    bundle {
        bnd """\
Bundle-Name: Web3j
Bundle-SymbolicName: \${project.group}.web3j
Bundle-Version: \${task.bundleVersion}
Export-Package: org.web3j.*, com.kenai.*, jnr.a64asm, jnr.x86asm
Import-Package: * 
-includeresource: \
    @core-${web3jVersion}.jar, \
    @jffi-1.3.9.jar, \
    @jnr-ffi-2.2.11.jar, \
    @jnr-a64asm-1.0.0.jar, \
    @jnr-x86asm-1.0.2.jar
"""
    }
}

publishing {
    publications {
        web3j(MavenPublication) {
            from components.java
        }
    }
}

artifactoryPublish {
    publications 'web3j'
}
