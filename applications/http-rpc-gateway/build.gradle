plugins {
    id 'com.r3.internal.gradle.plugins.r3Publish'
    id 'corda.common-app'
}

description 'HTTP RPC Gateway Application'

sourceSets {
    e2eTest {
        kotlin {
            srcDirs = [ 'src/e2e-test/kotlin' ]
        }
        java {
            srcDirs = [ 'src/e2e-test/java' ]
        }
        resources {
            srcDirs = [ 'src/e2e-test/resources' ]
        }
    }
}

configurations {
    e2eTestApi.extendsFrom integrationTestApi
    e2eTestCompileOnly.extendsFrom integrationTestCompileOnly
    e2eTestImplementation.extendsFrom integrationTestImplementation
    e2eTestRuntimeOnly.extendsFrom integrationTestRuntimeOnly
}

dependencies {
    compileOnly 'org.osgi:osgi.annotation'
    compileOnly 'org.osgi:osgi.core'
    compileOnly 'org.osgi:org.osgi.service.component.annotations'

    implementation platform("net.corda:corda-api:$cordaApiVersion")
    implementation "com.typesafe:config:$typeSafeConfigVersion"
    implementation "info.picocli:picocli:$picocliVersion"
    implementation 'net.corda.kotlin:kotlin-stdlib-jdk8-osgi'
    implementation 'org.slf4j:slf4j-api'
    implementation 'net.corda:corda-base'
    implementation project(":osgi-framework-api")

    implementation project(":components:configuration:configuration-read-service")

    implementation project(":libs:messaging:messaging")
    implementation project(":libs:lifecycle:lifecycle")
    implementation project(":libs:configuration:configuration-core")
    implementation project(':libs:http-rpc:http-rpc')
    implementation project(':libs:http-rpc:http-rpc-security-read')
    implementation project(':libs:http-rpc:ssl-cert-read')
    implementation project(':libs:http-rpc:http-rpc-server')

    // Components
    implementation project(":components:http-rpc-gateway-comp")

    // Implementations (needs revision!)
    runtimeOnly project(":libs:messaging:messaging-impl")
    runtimeOnly project(":libs:messaging:kafka-message-bus-impl")
    runtimeOnly project(":libs:permissions:permission-validation-impl")
    runtimeOnly project(":libs:permissions:permission-manager-impl")
    runtimeOnly project(":libs:permissions:permission-cache-impl")

    runtimeOnly project(":components:permissions:permission-cache-service")
    runtimeOnly project(":components:permissions:permission-validation-service")
    runtimeOnly project(":components:permissions:permission-management-service")
    runtimeOnly project(":components:permissions:permission-rpc-ops-impl")
    runtimeOnly project(":components:configuration:configuration-read-service-impl")

    runtimeOnly project(":libs:configuration:configuration-publish-impl")
    runtimeOnly project(":libs:configuration:file-configuration-read-impl")
    runtimeOnly project(":libs:kafka-utils:topic-admin-impl")
    runtimeOnly project(":libs:schema-registry:schema-registry-impl")

    runtimeOnly project(":libs:lifecycle:lifecycle-impl")
    runtimeOnly project(":libs:http-rpc:http-rpc-server-impl")
    runtimeOnly project(':libs:http-rpc:http-rpc-stubs-impl')

    runtimeOnly "org.apache.logging.log4j:log4j-slf4j-impl:$log4jVersion"
    runtimeOnly "org.apache.felix:org.apache.felix.scr:$felixScrVersion"
    runtimeOnly "org.osgi:org.osgi.util.function:$osgiUtilFunctionVersion"
    runtimeOnly "org.osgi:org.osgi.util.promise:$osgiUtilPromiseVersion"

    e2eTestImplementation project(":libs:http-rpc:http-rpc-client")
    e2eTestImplementation project(":libs:permissions:permission-endpoint")
    e2eTestImplementation project(":testing:test-utilities")
}


tasks.register('e2eTest', Test) {
    description = "Runs E2E tests."
    group = "verification"

    testClassesDirs = project.sourceSets["e2eTest"].output.classesDirs
    classpath = project.sourceSets["e2eTest"].runtimeClasspath
    outputs.upToDateWhen { false }
}
