plugins {
    id 'distribution'
    id 'org.jetbrains.kotlin.jvm'
    id 'org.jetbrains.kotlin.kapt'
    id 'corda.cli-plugin-packager'
    id 'corda.common-publishing'
}

ext {
    releasable = false
}

sourceSets {
    smokeTest {
        java {
            srcDirs += [ 'src/smokeTest/java' ]
        }
        kotlin {
            srcDirs += [ 'src/smokeTest/kotlin' ]
        }
        resources {
            srcDirs = [ 'src/smokeTest/resources' ]
        }
        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output
    }
}

kotlin {
    target {
        java
        compilations.smokeTest {
            associateWith compilations.main
            associateWith compilations.test

            configurations {
                smokeTestApi.extendsFrom testApi
                smokeTestImplementation.extendsFrom testImplementation
                smokeTestRuntimeOnly.extendsFrom testRuntimeOnly
            }
        }
    }
}

group 'net.corda.cli.deployment'

dependencies {
    compileOnly "net.corda.cli.host:api:$pluginHostVersion"

    implementation "com.fasterxml.jackson.module:jackson-module-kotlin:$jacksonVersion"
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:$jacksonVersion"
    implementation "org.yaml:snakeyaml:${snakeyamlVersion}"
    implementation "com.konghq:unirest-java:$unirestVersion"
    implementation project(":libs:crypto:certificate-generation")
    implementation project(':libs:crypto:cipher-suite')
    implementation "org.pf4j:pf4j:${pf4jVersion}"
    kapt "org.pf4j:pf4j:${pf4jVersion}"

    implementation project(":tools:plugins:package")
    implementation project(':tools:plugins:plugins-rest')
    implementation project(':components:membership:membership-rest')
    implementation project(':libs:virtual-node:cpi-upload-endpoints')
    implementation project(':libs:virtual-node:virtual-node-endpoints')
    implementation project(':libs:virtual-node:virtual-node-info')
    implementation project(':libs:configuration:configuration-endpoints')
    implementation project(':libs:membership:membership-common')

    testImplementation "net.corda.cli.host:api:$pluginHostVersion"
    testImplementation "org.pf4j:pf4j:${pf4jVersion}"
    testImplementation "org.junit.jupiter:junit-jupiter:${junit5Version}"
    testImplementation "com.github.stefanbirkner:system-lambda:1.2.1"

    smokeTestImplementation project(':testing:e2e-test-utilities')
}

cliPlugin {
    cliPluginClass = 'net.corda.cli.plugins.network.NetworkPluginWrapper'
    cliPluginDescription = 'Plugin for network operations.'
}

tasks.named("installDist") {
    dependsOn cliPlugin
    def homePath = System.properties['user.home']
    from cliPlugin
    into "$homePath/.corda/cli/plugins"
}

publishing {
    publications {
        maven(MavenPublication) {
            artifactId "${cliPlugin.cliPluginId.get()}-cli-plugin"
            groupId project.group
            artifact cliPluginTask
        }
    }
}

tasks.register('smokeTest', Test) {
    description = "Runs smoke tests for Corda CLI plugins."
    group = "verification"

//    dependsOn smokeTestResources

    testClassesDirs = project.sourceSets["smokeTest"].output.classesDirs
    classpath = project.sourceSets["smokeTest"].runtimeClasspath

    def combinedWorker = project.getProperties().getOrDefault("isCombinedWorker", true).toBoolean()
    println "Running tests against ${ combinedWorker ? "combined worker" : "Kubernetes cluster" }"

    // Note these port values have to match what is setup as part of port forwarding at cluster bootstrap time.
    // E.g. during Jenkins pipeline setup.
    def combinedWorkerUrl = "http://localhost:7004/"
    def cryptoWorkerUrl = project.getProperties().getOrDefault("cryptoWorkerUrl", combinedWorker ? combinedWorkerUrl : "http://localhost:7001/")
    systemProperty "cryptoWorkerUrl", cryptoWorkerUrl
    println "Crypto worker url: $cryptoWorkerUrl"
    def restWorkerUrl = project.getProperties().getOrDefault("restWorkerUrl", combinedWorker ? combinedWorkerUrl : "http://localhost:7002/")
    systemProperty "restWorkerUrl", restWorkerUrl
    println "REST worker url: $restWorkerUrl"
    def flowWorkerUrl = project.getProperties().getOrDefault("flowWorkerUrl", combinedWorker ? combinedWorkerUrl : "http://localhost:7003/")
    systemProperty "flowWorkerUrl",flowWorkerUrl
    println "Flow worker url: $flowWorkerUrl"
    def flowMapperWorkerUrl = project.getProperties().getOrDefault("flowMapperWorkerUrl", combinedWorker ? combinedWorkerUrl : "http://localhost:7004/")
    systemProperty "flowMapperWorkerUrl", flowMapperWorkerUrl
    println "Flow Mapper worker url: $flowMapperWorkerUrl"
    def verificationWorkerUrl  = project.getProperties().getOrDefault("verificationWorkerUrl", combinedWorker ? combinedWorkerUrl : "http://localhost:7005/")
    systemProperty "verificationWorkerUrl", verificationWorkerUrl
    println "Verification worker url: $verificationWorkerUrl"
    def dbWorkerUrl = project.getProperties().getOrDefault("dbWorkerUrl", combinedWorker ? combinedWorkerUrl : "http://localhost:7006/")
    systemProperty "dbWorkerUrl", dbWorkerUrl
    println "DB worker url: $dbWorkerUrl"
    def persistenceWorkerUrl = project.getProperties().getOrDefault("persistenceWorkerUrl", combinedWorker ? combinedWorkerUrl : "http://localhost:7007/")
    systemProperty "persistenceWorkerUrl", persistenceWorkerUrl
    println "Persistence worker url: $persistenceWorkerUrl"
    def uniquenessWorkerUrl = project.getProperties().getOrDefault("uniquenessWorkerUrl", combinedWorker ? combinedWorkerUrl : "http://localhost:7008/")
    systemProperty "uniquenessWorkerUrl", uniquenessWorkerUrl

    jvmArgs '--add-opens', 'java.base/java.lang.reflect=ALL-UNNAMED'
}