import aQute.bnd.gradle.Bundle

plugins {
    id 'corda.common-publishing'
    id 'corda.osgi-test-conventions'
    id 'corda.common-library'
}

description 'Corda ledger verification'

configurations {
    cpis {
        canBeConsumed = false
        transitive = false
    }
}

dependencies {
    compileOnly 'org.osgi:org.osgi.service.component.annotations'
    compileOnly "org.osgi:org.osgi.service.component:$osgiServiceComponentVersion"

    implementation platform("net.corda:corda-api:$cordaApiVersion")

    implementation 'net.corda:corda-config-schema'
    implementation 'net.corda:corda-ledger-common'
    implementation 'net.corda:corda-ledger-utxo'
    implementation 'net.corda:corda-topic-schema'
    implementation 'org.jetbrains.kotlin:kotlin-osgi-bundle'
    implementation 'org.slf4j:slf4j-api'

    implementation project(':components:configuration:configuration-read-service')
    implementation project(':components:virtual-node:cpk-read-service')
    implementation project(':components:virtual-node:sandbox-group-context-service')

    implementation project(':libs:crypto:cipher-suite')
    implementation project(':libs:flows:external-event-responses')
    implementation project(':libs:ledger:ledger-common-data')
    implementation project(':libs:ledger:ledger-utxo-data')
    implementation project(':libs:ledger:ledger-utxo-transaction-verifier')
    implementation project(':libs:messaging:messaging')
    implementation project(":libs:serialization:json-validator")
    implementation project(':libs:utilities')
    implementation project(':libs:virtual-node:sandbox-group-context')

    runtimeOnly project(':components:virtual-node:sandbox-amqp')
    runtimeOnly project(':components:virtual-node:sandbox-json')
    runtimeOnly project(":libs:crypto:crypto-serialization-impl")
    runtimeOnly project(':libs:crypto:merkle-impl')

    testImplementation "org.mockito.kotlin:mockito-kotlin:$mockitoKotlinVersion"

    testImplementation project(':libs:ledger:ledger-utxo-data')
    testImplementation project(":libs:lifecycle:lifecycle-test-impl")
    testImplementation project(":libs:lifecycle:lifecycle-impl")
    testImplementation project(":libs:lifecycle:registry")

    // Integration test resources using cpis configuration
    cpis project(path: ':testing:cpbs:ledger-utxo-demo-app', configuration: 'cordaCPB')

    // IMPORTANT: do NOT include cpbs (or cpks) as dependencies of the integration test.
    // This would cause OSGi to load the bundle twice (once as part of the test) and again
    // as part of the CPB loading.  This introduces *two* unique instances of classes in the CPKs (remember
    // the class is unique by name **and** classloader).

    // IMPORTANT:  do NOT attempt to use mockito-kotlin in the integration tests.
    // It's not an OSGi bundle, so you will get errors (despite Intellij appearing to allow you to use it).

    integrationTestImplementation("org.mockito:mockito-core:$mockitoVersion")
    integrationTestImplementation("com.fasterxml.jackson.module:jackson-module-kotlin:$jacksonVersion")

    integrationTestImplementation project(':components:virtual-node:cpi-info-read-service')
    integrationTestImplementation project(':components:virtual-node:virtual-node-info-read-service')
    integrationTestImplementation project(':libs:ledger:ledger-common-data')
    integrationTestImplementation project(':libs:serialization:serialization-amqp')
    integrationTestImplementation project(':libs:serialization:serialization-internal')
    integrationTestImplementation project(':testing:db-message-bus-testkit')
    integrationTestImplementation project(':testing:ledger:ledger-common-testkit')
    integrationTestImplementation project(':testing:sandboxes')
    integrationTestImplementation project(':testing:test-serialization')
    integrationTestImplementation project(':testing:test-utilities')

    // needed to import serialization libs
    integrationTestRuntimeOnly "org.apache.aries.spifly:org.apache.aries.spifly.dynamic.framework.extension:$ariesDynamicFrameworkExtensionVersion"

    integrationTestRuntimeOnly project(':libs:flows:external-event-responses-impl')
    integrationTestRuntimeOnly project(':libs:messaging:db-message-bus-impl')
    integrationTestRuntimeOnly project(':libs:messaging:messaging-impl')

    integrationTestRuntimeOnly "com.sun.activation:javax.activation:$activationVersion"
}

//  Copy the cpi builds declared in the cpis configuration into our resources so we find and load them
def integrationTestResources = tasks.named('processIntegrationTestResources', ProcessResources) {
    from(configurations.cpis) {
        into 'META-INF'
        rename "(.*)-\\Q${version}\\E-package.cpb", "\$1.cpb"
    }
}

tasks.named('testingBundle', Bundle) {
    dependsOn integrationTestResources
}
