plugins{
    id 'distribution'
    id 'org.jetbrains.kotlin.kapt'
    id 'java-library'
    id 'idea'
    id 'io.gitlab.arturbosch.detekt'
    id 'jacoco'
}

version rootProject.version
group 'net.corda.cli.deployment'

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib"
    implementation("org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion")

    compileOnly "org.pf4j:pf4j:$pf4jVersion"
    compileOnly "net.corda.cli.host:api:$pluginHostVersion"
    kapt "org.pf4j:pf4j:$pf4jVersion"
    kapt "info.picocli:picocli-codegen:$picoCliVersion"

    implementation platform("net.corda:corda-api:$cordaApiVersion")
    implementation "net.corda:corda-db-schema:$cordaApiVersion"
    implementation "org.liquibase:liquibase-core:$liquibaseVersion"

    testImplementation "org.junit.jupiter:junit-jupiter:$junit5Version"
    testImplementation "org.mockito.kotlin:mockito-kotlin:$mockitoKotlinVersion"
    testImplementation "org.pf4j:pf4j:$pf4jVersion"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junit5Version"
    testCompileOnly "net.corda.cli.host:api:$pluginHostVersion"
}

def pluginJar = tasks.named('jar', Jar) {
    manifest {
        attributes['Plugin-Class'] = pluginClass
        attributes['Plugin-Id'] = pluginId
        attributes['Plugin-Provider'] = pluginProvider
        attributes['Plugin-Description'] = pluginDescription
        attributes['Plugin-Version'] = version
    }
}

def plugin = tasks.register('plugin', Zip) {
    archiveBaseName = "plugin-$pluginId-$version"
    // first taking the classes generated by the jar task
    into('classes') {
        with pluginJar.get()
    }
    // and then we also need to include any libraries that are needed by the plugin
    dependsOn configurations.runtimeClasspath.buildDependencies
    into('lib') {
        from configurations.runtimeClasspath.filter { it.name.endsWith('jar')}
    }
}

tasks.named("installDist").configure{
    dependsOn 'plugin'
    def homePath = System.properties['user.home']
    from tasks.named('plugin')
    into "$homePath/.corda/cli/plugins"
}

artifacts {
    archives plugin
}
