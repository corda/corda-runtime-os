plugins {
    id 'biz.aQute.bnd.builder'
    id 'corda.common-library'
    id 'corda.common-publishing'
}

description 'JSON validator Impl'

configurations {
    bundle {
        canBeResolved = false
    }
    bundle.extendsFrom runtimeClasspath
}

dependencies {
    compileOnly 'org.osgi:osgi.annotation'
    compileOnly 'org.osgi:org.osgi.service.component.annotations'

    implementation platform("net.corda:corda-api:$cordaApiVersion")

    implementation project(":libs:serialization:json-validator-common")
    implementation "com.networknt:json-schema-validator:$networkntJsonSchemaVersion"
    implementation 'org.jetbrains.kotlin:kotlin-stdlib'
    implementation "io.github.erdtman:java-json-canonicalization:$jsonCanonicalizerVersion"

    constraints {
        implementation("com.ethlo.time:itu:$comEthloTimeItuVersion") {
            because "Version bundled with current version of 'com.networknt:json-schema-validator' does not have OSGi manifest."
        }
    }
}

def jar = tasks.named('jar', Jar) {
    archiveBaseName = 'corda-json-validator-common-impl'

    bundle {
        bnd """\
Bundle-Name: Corda JSON validator
Bundle-SymbolicName: \${project.group}.json-validator-common-impl
Import-Package:\
    !org.apache.hadoop.io.compress,\
    sun.misc;resolution:=optional,\
    *
-includeresource: @java-json-canonicalization-${jsonCanonicalizerVersion}.jar
"""
    }
}

artifacts {
    bundle jar
}

