import aQute.bnd.gradle.Bundle
import aQute.bnd.gradle.Resolve
import aQute.bnd.gradle.TestOSGi
import aQute.bnd.version.MavenVersion

plugins {
    id 'corda.common-library'
    id 'com.r3.internal.gradle.plugins.r3Publish'
//    id 'corda.osgi-test-conventions'
}

description 'CPI Store integration tests'

evaluationDependsOn('test-resources:contract-cpk')
evaluationDependsOn('test-resources:workflow-cpk')

sourceSets {
    kafkaIntegrationTest {
        kotlin {
            srcDirs = [ 'src/integration-test/kotlin' ]
        }
        resources {
            srcDirs = [ 'src/integration-test/resources' ]
        }
    }
}

configurations {
    kafkaIntegrationTestApi.extendsFrom integrationTestApi
    kafkaIntegrationTestCompileOnly.extendsFrom integrationTestCompileOnly
    kafkaIntegrationTestImplementation.extendsFrom integrationTestImplementation
    kafkaIntegrationTestRuntimeOnly.extendsFrom integrationTestRuntimeOnly
}

dependencies {
    compileOnly 'org.osgi:osgi.core'
    compileOnly 'org.osgi:org.osgi.service.component.annotations'

    implementation project(":libs:lifecycle:lifecycle")
    implementation project(":libs:messaging:messaging")
    implementation project(":libs:schema-registry:schema-registry")
    implementation project(":libs:cpi-store:cpi-read")
    implementation project(":libs:cpi-store:cpi-write")
    implementation project(":libs:cpi-store:cpi-read-impl-kafka")
    implementation project(":libs:cpi-store:cpi-write-impl-kafka")
    implementation project(":libs:cpi-store:cpi-utils")
    implementation project(":libs:configuration:configuration-core")


    implementation platform("net.corda:corda-api:$cordaApiVersion")
    implementation "net.corda:corda-avro-schema"
    implementation "net.corda:corda-base"
    implementation 'net.corda.kotlin:kotlin-stdlib-jdk8-osgi'
    implementation "net.corda:corda-packaging"

    implementation "com.typesafe:config:$typeSafeConfigVersion"
    implementation "org.apache.servicemix.bundles:org.apache.servicemix.bundles.kafka-clients:$kafkaClientVersion"

    testImplementation "org.assertj:assertj-core:$assertjVersion"
    testImplementation "org.mockito.kotlin:mockito-kotlin:$mockitoKotlinVersion"
    testImplementation project(":testing:test-utilities")

    implementation project(":libs:cpi-store:integration-test:test-resources:contract-cpk")
    implementation project(":libs:cpi-store:integration-test:test-resources:workflow-cpk")

    testRuntimeOnly 'org.osgi:osgi.core'

    kafkaIntegrationTestImplementation project(":libs:messaging:kafka-messaging-impl")
    kafkaIntegrationTestImplementation project(":components:kafka-topic-admin")
    kafkaIntegrationTestImplementation "org.osgi:org.osgi.test.junit5:$osgiTestJunit5Version"

    //runtime implementation dependencies
    kafkaIntegrationTestRuntimeOnly project(":libs:schema-registry:schema-registry-impl")
    kafkaIntegrationTestRuntimeOnly project(":libs:kafka-utils:topic-admin-impl")
    kafkaIntegrationTestRuntimeOnly project(":libs:lifecycle:lifecycle-impl")
    kafkaIntegrationTestRuntimeOnly project(":libs:cpi-store:cpi-read-impl-file")
    kafkaIntegrationTestRuntimeOnly project(":libs:cpi-store:cpi-utils")
    kafkaIntegrationTestRuntimeOnly "org.junit.platform:junit-platform-launcher:$junitPlatformVersion"
    kafkaIntegrationTestRuntimeOnly "org.apache.felix:org.apache.felix.framework:$felixVersion"
    kafkaIntegrationTestRuntimeOnly "org.apache.felix:org.apache.felix.scr:$felixScrVersion"
    kafkaIntegrationTestRuntimeOnly "org.osgi:org.osgi.util.promise:$osgiUtilPromiseVersion"
    kafkaIntegrationTestRuntimeOnly "org.osgi:org.osgi.util.function:$osgiUtilFunctionVersion"
    kafkaIntegrationTestRuntimeOnly "org.slf4j:slf4j-simple:$slf4jVersion"
    kafkaIntegrationTestRuntimeOnly "net.corda:corda-packaging"
    kafkaIntegrationTestRuntimeOnly project(path: ':libs:cpi-store:integration-test:test-resources:workflow-cpk', configuration: 'outputcpb')
}

def kafkaTestingBundle = tasks.register('kafkaTestingBundle', Bundle) {
    archiveClassifier = 'tests'
    from sourceSets.kafkaIntegrationTest.output
    sourceSet = sourceSets.kafkaIntegrationTest

    bnd """\
Bundle-SymbolicName: \${task.archiveBaseName}-\${task.archiveClassifier}
Test-Cases: \${classes;HIERARCHY_INDIRECTLY_ANNOTATED;org.junit.platform.commons.annotation.Testable;CONCRETE}
"""
}

def resolve = tasks.register('resolve', Resolve) {
    dependsOn jar, kafkaTestingBundle
    bundles = files(sourceSets.kafkaIntegrationTest.runtimeClasspath, configurations.archives.artifacts.files)
    bndrun = file('test.bndrun')
    //  bnd attempts to use ~/ for caching if this is unavailable the build will fail.
    System.setProperty("bnd.home.dir", "$rootDir/bnd/")
}

Provider<Task> cpkTaskProvider = project(':libs:cpi-store:integration-test:test-resources:workflow-cpk').tasks.named('cpb')
ext {
    cpiPath = cpkTaskProvider.get().outputs.files.asPath
}

tasks.register('kafkaIntegrationTest', TestOSGi) {
    description = "Runs Kafka OSGi integration tests."
    group = "verification"
    dependsOn resolve
    bundles = files(sourceSets.kafkaIntegrationTest.runtimeClasspath, configurations.archives.artifacts.files)
    bndrun = file('test.bndrun')
}

artifacts {
    archives kafkaTestingBundle
}





