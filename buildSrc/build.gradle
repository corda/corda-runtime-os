plugins {
    id 'groovy-gradle-plugin'
}

// We can't get the values directly from `gradle.properties` from here for some gradle-y reason.
// So we'll load it into our own object to grab what we need.
def constants = new Properties()
file("$rootDir/../gradle.properties").withInputStream { InputStream input -> constants.load(input) }
Properties cordaInternalDependencies = new Properties()
file("$projectDir/../cordaInternalDependencies.properties").withInputStream { InputStream input -> cordaInternalDependencies.load(input) }
def bndVersion = constants.getProperty('bndVersion')
def artifactoryContextUrl = constants.getProperty('artifactoryContextUrl')
def jibCoreVersion = constants.getProperty('jibCoreVersion')
def internalPublishVersion = cordaInternalDependencies.getProperty('internalPublishVersion')
def gradleEnterpriseVersion = constants.getProperty('gradleEnterpriseVersion')

dependencies {
    implementation "biz.aQute.bnd:biz.aQute.bnd.gradle:$bndVersion"
    implementation "biz.aQute.bnd:biz.aQute.bndlib:$bndVersion"

    implementation "com.google.cloud.tools:jib-core:$jibCoreVersion"
    implementation "com.gradle:gradle-enterprise-gradle-plugin:$gradleEnterpriseVersion"

    if (System.getenv('CORDA_ARTIFACTORY_USERNAME') != null || project.hasProperty('cordaArtifactoryUsername')) {
        implementation "com.r3.internal.gradle.plugins:publish:$internalPublishVersion"
    }
}

repositories {
    def cordaUseCache = System.getenv("CORDA_USE_CACHE")
    if (cordaUseCache != null) {
        maven {
            url = "${artifactoryContextUrl}/${cordaUseCache}"
            name = "R3 Maven remote repositories"
            authentication {
                basic(BasicAuthentication)
            }
            credentials {
                username = findProperty('cordaArtifactoryUsername') ?: System.getenv('CORDA_ARTIFACTORY_USERNAME')
                password = findProperty('cordaArtifactoryPassword') ?: System.getenv('CORDA_ARTIFACTORY_PASSWORD')
            }
        }
    } else {
        //needed for kotlin osgi bundles generated by us
        maven {
            url = "${artifactoryContextUrl}/corda-dependencies"
        }
        maven {
            url "${artifactoryContextUrl}/engineering-tools-maven"
            authentication {
                basic(BasicAuthentication)
            }
            credentials {
                username = findProperty('cordaArtifactoryUsername') ?: System.getenv('CORDA_ARTIFACTORY_USERNAME')
                password = findProperty('cordaArtifactoryPassword') ?: System.getenv('CORDA_ARTIFACTORY_PASSWORD')
            }
            content {
                includeGroupByRegex 'com\\.r3\\.internal(\\..*)?'
            }
        }
        gradlePluginPortal()
    }
}
