plugins {
    id 'java-library'
}

description 'OSGi Framework Bootstrap'

configurations {
    testBundles {
        attributes { attrs ->
            attrs.attribute(LibraryElements.LIBRARY_ELEMENTS_ATTRIBUTE, objects.named(LibraryElements, LibraryElements.JAR))
        }
        canBeConsumed = false
        transitive = false
    }
}

dependencies {
    compileOnly 'org.osgi:osgi.core'
    implementation platform("net.corda:corda-api:$cordaApiVersion")
    implementation project(':osgi-framework-api')
    implementation libs.slf4j.api
    implementation libs.slf4j.jul
    runtimeOnly "org.apache.felix:org.apache.felix.framework:$felixVersion"
    runtimeOnly libs.log4j.slf4j
    runtimeOnly libs.log4j.layout.json

    // Jackson dependencies for JSON formatted logs
    runtimeOnly libs.jackson.core
    runtimeOnly libs.jackson.databind
    runtimeOnly libs.jackson.annotations

    testImplementation libs.mockito.core
    testImplementation "org.apache.sling:org.apache.sling.testing.osgi-mock.junit5:$slingVersion"
    testImplementation libs.junit.api
    testImplementation libs.assertj.core
    testRuntimeOnly libs.junit.engine

    testBundles project(':testing:apps:test-app')
}

def copyBundles = tasks.register('copyBundles', Copy) {
    into layout.buildDirectory.dir('resources/test/bundles')
    from configurations.testBundles
}

tasks.named('test', Test) {
    dependsOn copyBundles
    doFirst {
        def applicationBundleSet = configurations.testBundles.files.collect { "bundles/${it.name}" } as Set<String>
        final applicationBundlesFile = new File(project.buildDir, "resources/test/application_bundles")
        applicationBundlesFile.withWriter { writer ->
            applicationBundleSet.each { line ->
                writer.writeLine(line)
            }
        }
        final numberOfBundles = applicationBundleSet.size()
        logger.info "Included $numberOfBundles OSGi bundles in $applicationBundlesFile resource."
    }
}
