import aQute.bnd.gradle.Bundle

plugins {
    id 'org.jetbrains.kotlin.jvm'
}

description 'Worker Smoketests'

configurations {
    cpis {
        canBeConsumed = false
        transitive = false
    }
}

sourceSets {
    smokeTest {
        java {
            srcDirs += [ 'src/smokeTest/java' ]
        }
        kotlin {
            srcDirs += [ 'src/smokeTest/kotlin' ]
        }
        resources {
            srcDirs = [ 'src/smokeTest/resources' ]
        }
        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output
    }
}

kotlin {
    target {
        java
        compilations.smokeTest {
            associateWith compilations.main
            associateWith compilations.test

            configurations {
                smokeTestApi.extendsFrom testApi
                smokeTestImplementation.extendsFrom testImplementation
                smokeTestRuntimeOnly.extendsFrom testRuntimeOnly
            }
        }
    }
}

dependencies {
    // NO CORDA DEPENDENCIES!!
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core-jvm:1.4.2"

    // But building a cpb for use in a test is ok.
    cpis project(path: ':testing:cpbs:calculator', configuration: 'cordaCPB')

    smokeTestImplementation "com.konghq:unirest-java:$unirestVersion"
    smokeTestImplementation "com.konghq:unirest-objectmapper-jackson:$unirestVersion"
    smokeTestImplementation "com.fasterxml.jackson.module:jackson-module-kotlin:$jacksonVersion"
    smokeTestImplementation project(':testing:test-utilities')

    smokeTestImplementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:$jacksonVersion"
    smokeTestImplementation "com.fasterxml.jackson.module:jackson-module-kotlin:$jacksonVersion"
    smokeTestImplementation "org.assertj:assertj-core:$assertjVersion"
    smokeTestImplementation "org.junit.jupiter:junit-jupiter-api:$junit5Version"
    smokeTestRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junit5Version"
}

def smokeTestResources = tasks.named('processSmokeTestResources', ProcessResources) {
    // Copy the cpb(s) into resources, and strip out the version-package part so it's easier to reference in the
    // smoke tests.
    from(configurations.cpis) {
        into 'META-INF'
        rename "(.+)-\\d+.+-package.cpb", "\$1.cpb"
    }
}

tasks.register('smokeTest', Test) {
    description = "Runs smoke tests."
    group = "verification"

    testClassesDirs = project.sourceSets["smokeTest"].output.classesDirs
    classpath = project.sourceSets["smokeTest"].runtimeClasspath

    systemProperty "rpcHost", project.getProperties().getOrDefault("rpcHost","https://localhost:8888/")
    systemProperty "cryptoWorkerHealthHttp",
            project.getProperties().getOrDefault("cryptoWorkerHealthHttp","http://localhost:7001/")
    systemProperty "dbWorkerHealthHttp",
            project.getProperties().getOrDefault("dbWorkerHealthHttp","http://localhost:7002/")
    systemProperty "flowWorkerHealthHttp",
            project.getProperties().getOrDefault("flowWorkerHealthHttp","http://localhost:7003/")
    systemProperty "rpcWorkerHealthHttp",
            project.getProperties().getOrDefault("rpcWorkerHealthHttp","http://localhost:7004/")
}


tasks.named('smokeTest', Test) {
    dependsOn smokeTestResources
}
