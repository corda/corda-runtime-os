plugins {
    id 'corda.common-publishing'
    id 'corda.common-library'
    id 'application'
}

description "P2P Testing tools - P2P layer deployment"

dependencies {
    implementation platform('org.jetbrains.kotlin:kotlin-bom')
    implementation project(':libs:crypto:certificate-generation')
    implementation project(':libs:p2p-crypto')
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    implementation "info.picocli:picocli:$picocliVersion"
    implementation "net.corda:corda-topic-schema"
    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:$jacksonVersion"
    implementation "com.konghq:unirest-java:$unirestVersion"
    implementation platform("net.corda:corda-api:$cordaApiVersion")
    implementation 'net.corda:corda-avro-schema'
    implementation 'net.corda:corda-cipher-suite'
    testImplementation "com.typesafe:config:$typeSafeConfigVersion"
    testRuntimeOnly project(':applications:tools:p2p-test:p2p-setup')
    runtimeOnly "org.apache.logging.log4j:log4j-slf4j-impl:$log4jVersion"
}
application {
    mainClass = 'net.corda.p2p.deployment.GoKt'
}

def binaryProjects = [
        'p2p-test:p2p-setup',
        'kafka-setup'
]

binaryProjects.forEach {name->
    def shortName = name.split(':').last()
    def copyJars = tasks.register("copyJars$shortName", Copy) {
        def project = ":applications:tools:$name"
        def task = rootProject.findProject(project).tasks.getByName('appJar')
        dependsOn(task)
        from task.outputs.files.first()
        into "$buildDir/jars/"
        rename {
            "${shortName}.jar"
        }
    }
    processResources.dependsOn(copyJars)
}
jacocoTestReport {
    dependsOn test
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, includes: [
                    "net/corda/**",
            ])
        }))
    }
}
sourceSets {
    main {
        resources {
            srcDirs "$buildDir/jars"
        }
    }
}
