plugins {
    id 'distribution'
    id 'org.jetbrains.kotlin.jvm'
    id 'org.jetbrains.kotlin.kapt'
    id 'corda.cli-plugin-packager'
    id 'corda.common-publishing'
}


group 'net.corda.cli.deployment'

dependencies {
    compileOnly libs.pf4j
    compileOnly "net.corda.cli.host:api:$pluginHostVersion"

    kapt libs.pf4j
    kapt "info.picocli:picocli:$picocliVersion"

    testImplementation libs.bundles.test
    testImplementation libs.pf4j
    testCompileOnly "net.corda.cli.host:api:$pluginHostVersion"
    testImplementation "com.github.stefanbirkner:system-lambda:$systemLambdaVersion"
    testImplementation "info.picocli:picocli:$picocliVersion"

    implementation libs.jackson.dataformat.yaml
    implementation libs.jackson.databind
    implementation libs.jackson.module.kotlin
    implementation libs.kubernetes.client
    constraints {
        implementation("com.squareup.okio:okio:$okioVersion") {
            because 'kubernetes-client uses an older version of Okio library, needs to be at least 3.4.0 to avoid CVE-2023-0833.'
        }
        implementation("com.squareup.okhttp3:okhttp:$okHttpVersion") {
            because 'kubernetes-client uses an older version of okhttp3 library, needs to be at least 4.9.2 to avoid CVE-2023-0833.'
        }
    }
    implementation libs.postgresql.jdbc

    implementation "org.apache.servicemix.bundles:org.apache.servicemix.bundles.kafka-clients:$kafkaClientVersion"
    constraints {
        implementation("org.xerial.snappy:snappy-java:$xerialSnappyVersion") {
            because 'Kafka Client uses an older version of Snappy library which is exposed to CVE-2023-34455. ' +
                    'This might be resolved in the future versions of Kafka Client.'
        }
    }
}

cliPlugin {
    cliPluginClass = 'net.corda.cli.plugins.preinstall.PreInstallPlugin'
    cliPluginDescription = 'Plugin for pre-install checks.'
}
