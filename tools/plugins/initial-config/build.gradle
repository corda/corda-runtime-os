plugins {
    id 'distribution'
    id 'org.jetbrains.kotlin.jvm'
    id 'org.jetbrains.kotlin.kapt'
}

group 'net.corda.cli.deployment'

dependencies {
    implementation platform("net.corda:corda-api:$cordaApiVersion")
    implementation 'net.corda.kotlin:kotlin-stdlib-jdk8-osgi'
    implementation 'javax.persistence:javax.persistence-api'

    compileOnly "org.pf4j:pf4j:$pf4jVersion"
    kapt "org.pf4j:pf4j:$pf4jVersion"
    kapt "info.picocli:picocli-codegen:$picocliVersion"

    implementation project(':libs:permissions:permission-datamodel')
    implementation project(':libs:permissions:permission-password')
    implementation project(':libs:configuration:configuration-datamodel')
    implementation project(':libs:configuration:configuration-core')
    implementation project(':libs:db:db-core')
    compileOnly "net.corda.cli.host:api:$pluginHostVersion"

    testImplementation "org.pf4j:pf4j:$pf4jVersion"
    testImplementation "com.github.stefanbirkner:system-lambda:$systemLambdaVersion"
    testImplementation "net.corda.cli.host:api:$pluginHostVersion"
}

// the plugin task will put the files into a jar file with the correct manifest
def plugin = tasks.register('plugin', Jar) {

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    archiveBaseName = "plugin-$pluginId-$version"

    manifest {
        attributes['Plugin-Class'] = pluginClass
        attributes['Plugin-Id'] = pluginId
        attributes['Plugin-Version'] = version
        attributes['Plugin-Provider'] = pluginProvider
        attributes['Plugin-Description'] = pluginDescription
    }

    from sourceSets.main.output

    dependsOn configurations.runtimeClasspath
    from {
        configurations.runtimeClasspath.findAll { it.name.endsWith('jar') }.collect { zipTree(it) }
    }
}

tasks.register('movePlugin', Copy) {
    from plugin
    into layout.buildDirectory.dir('plugin')
}

tasks.named('assemble'){
    dependsOn movePlugin
}

artifacts {
    archives plugin
}
