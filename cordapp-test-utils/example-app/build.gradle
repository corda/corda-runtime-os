import static org.gradle.api.JavaVersion.VERSION_11

plugins {
    id 'maven-publish'
    id "java-library"

    id 'org.jetbrains.kotlin.jvm'
    id 'net.corda.plugins.cordapp-cpk'
    id 'net.corda.plugins.cordapp-cpb'
    id 'net.corda.plugins.quasar-utils'
    id 'org.jetbrains.kotlin.plugin.jpa'
    id 'org.jetbrains.kotlin.plugin.allopen'
}

group = "net.cordacon"
version = "$cordaApiVersion"

def javaVersion = VERSION_11

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        languageVersion = '1.6'
        apiVersion = '1.6'
        jvmTarget = javaVersion
        javaParameters = true   // Useful for reflection.
        freeCompilerArgs += [
                "-java-parameters",
                "-Xjvm-default=all"
        ]
    }
}

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url = "$artifactoryContextUrl/corda-dependencies"

    }
    maven {
        url "${artifactoryContextUrl}/corda-os-maven"
        authentication {
            basic(BasicAuthentication)
        }
        credentials {
            username = System.getenv('CORDA_ARTIFACTORY_USERNAME')
            password = System.getenv('CORDA_ARTIFACTORY_PASSWORD')
        }
    }
    gradlePluginPortal()
}

configurations {
    implementation.transitive = false

    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntimeOnly.extendsFrom runtimeOnly
}

dependencies {

    cordaProvided 'net.corda.kotlin:kotlin-stdlib-jdk8-osgi'
    cordaProvided platform("net.corda:corda-api:$cordaApiVersion")
    cordaProvided 'net.corda:corda-base'
    cordaProvided 'net.corda:corda-application'
    cordaProvided 'org.slf4j:slf4j-api'

    runtimeOnly "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion"
    runtimeOnly "com.fasterxml.jackson.module:jackson-module-kotlin:$jacksonVersion"

    testImplementation project(':cordapp-test-utils:test-utils')
    testImplementation "org.junit.jupiter:junit-jupiter:$junit5Version"
    testImplementation "org.mockito:mockito-core:$mockitoVersion"
    testImplementation "org.mockito.kotlin:mockito-kotlin:$mockitoKotlinVersion"
    testImplementation "org.hamcrest:hamcrest-library:$hamcrestVersion"
}

check.dependsOn integrationTest

tasks.withType(Test) {
    useJUnitPlatform()
}

jar {
    baseName "cordapp-example-rollcall"
}

cordapp {
    targetPlatformVersion platformVersion as Integer
    minimumPlatformVersion platformVersion as Integer
    workflow
            {
                name jar.baseName
                versionId 1
                vendor "LizR3"
            }
}