plugins {
    id 'corda.common-library'
    id 'corda.osgi-test-conventions'
    id 'corda.common-publishing'
}

description 'LinkManager for p2p communication'

configurations {
    nonOsgiIntegrationTestApi.extendsFrom testApi
    nonOsgiIntegrationTestCompileOnly.extendsFrom testCompileOnly
    nonOsgiIntegrationTestImplementation.extendsFrom testImplementation
    nonOsgiIntegrationTestRuntimeOnly.extendsFrom testRuntimeOnly
}

sourceSets {
    nonOsgiIntegrationTest {
        kotlin {
            srcDirs = [ 'src/nonOsgiIntegrationTest/kotlin' ]
        }
        resources {
            srcDirs = [ 'src/nonOsgiIntegrationTest/resources' ]
        }
        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output
    }
}

dependencies {
    compileOnly 'org.osgi:osgi.annotation'
    compileOnly 'org.osgi:org.osgi.service.component.annotations'

    implementation 'net.corda.kotlin:kotlin-stdlib-jdk8-osgi'
    implementation project(":libs:p2p-crypto")
    implementation project(":libs:messaging:messaging")
    implementation project(":libs:lifecycle:lifecycle")
    implementation project(":libs:configuration:configuration-core")
    implementation project(":components:configuration:configuration-read-service")
    implementation project(":libs:configuration:configuration-schema:p2p")
    implementation project(':components:domino-logic')
    implementation project(':components:p2p-test:stub-crypto-processor')
    implementation "net.corda:corda-cipher-suite"

    runtimeOnly "org.apache.felix:org.apache.felix.scr:$felixScrVersion"
    runtimeOnly "org.osgi:org.osgi.util.function:$osgiUtilFunctionVersion"
    runtimeOnly "org.osgi:org.osgi.util.promise:$osgiUtilPromiseVersion"

    implementation platform("net.corda:corda-api:$cordaApiVersion")
    implementation "net.corda:corda-base"
    implementation "net.corda:corda-avro-schema"
    implementation "net.corda:corda-config-schema"
    implementation "net.corda:corda-topic-schema"
    implementation "com.typesafe:config:$typeSafeConfigVersion"

    testImplementation project(":testing:test-utilities")
    testImplementation "org.mockito:mockito-inline:$mockitoInlineVersion"
    testImplementation "org.mockito.kotlin:mockito-kotlin:$mockitoKotlinVersion"
    testImplementation "org.apache.logging.log4j:log4j-core:$log4jVersion"
    testRuntimeOnly "org.apache.logging.log4j:log4j-slf4j-impl:$log4jVersion"

    integrationTestImplementation project(":components:gateway")
    integrationTestImplementation project(":libs:configuration:configuration-publish")
    integrationTestImplementation project(":testing:db-message-bus-testkit")

    nonOsgiIntegrationTestImplementation platform("net.corda:corda-api:$cordaApiVersion")
    nonOsgiIntegrationTestImplementation project(":components:configuration:configuration-read-service-impl")
    nonOsgiIntegrationTestImplementation project(":components:gateway")
    nonOsgiIntegrationTestImplementation project(":libs:configuration:configuration-publish")
    nonOsgiIntegrationTestImplementation project(":libs:configuration:configuration-schema:p2p")
    nonOsgiIntegrationTestImplementation project(":libs:lifecycle:lifecycle-impl")
    nonOsgiIntegrationTestImplementation project(":libs:lifecycle:registry")
    nonOsgiIntegrationTestImplementation project(":libs:configuration:configuration-core")
    nonOsgiIntegrationTestImplementation project(":libs:configuration:configuration-publish-impl")
    nonOsgiIntegrationTestImplementation project(":testing:p2p:inmemory-messaging-impl")

    integrationTestImplementation project(":components:configuration:configuration-read-service-impl")
    integrationTestRuntimeOnly project(":libs:configuration:configuration-publish-impl")
    integrationTestRuntimeOnly project(":libs:lifecycle:lifecycle-impl")
    integrationTestRuntimeOnly project(":libs:messaging:db-message-bus-impl")
    integrationTestRuntimeOnly project(":libs:messaging:db-topic-admin-impl")
    integrationTestRuntimeOnly project(":libs:messaging:messaging-impl")
    integrationTestRuntimeOnly project(":libs:schema-registry:schema-registry-impl")

    testRuntimeOnly "com.lmax:disruptor:$disruptorVersion"
    testRuntimeOnly "com.sun.activation:javax.activation:$activationVersion"
    testRuntimeOnly "org.apache.aries.spifly:org.apache.aries.spifly.dynamic.bundle:$ariesDynamicBundleVersion"
    testRuntimeOnly "org.hsqldb:hsqldb:$hsqldbVersion"
    testRuntimeOnly "org.postgresql:postgresql:$postgresDriverVersion"
}

var nonOsgiIntegrationTest = tasks.register('nonOsgiIntegrationTest', Test) {
    description = "Runs non-OSGi Integration tests."
    group = "verification"

    testClassesDirs = project.sourceSets["nonOsgiIntegrationTest"].output.classesDirs
    classpath = project.sourceSets["nonOsgiIntegrationTest"].runtimeClasspath

    doFirst {
        def hostsFilePath = file("${sourceSets.nonOsgiIntegrationTest.output.resourcesDir}/hosts").absolutePath
        systemProperty 'jdk.net.hosts.file', hostsFilePath
    }
}

tasks.named('integrationTest') {
    dependsOn nonOsgiIntegrationTest
}
