import org.jetbrains.kotlin.gradle.tasks.KotlinCompile
import static org.gradle.api.JavaVersion.VERSION_11
import io.gitlab.arturbosch.detekt.DetektPlugin

buildscript {
    repositories {
        maven {
            url "$artifactoryContextUrl/corda-releases"
        }
    }
}

plugins {
    id 'org.jetbrains.kotlin.jvm'
    id 'org.jetbrains.kotlin.plugin.allopen' apply false
    id 'org.jetbrains.kotlin.plugin.noarg' apply false
    id 'io.gitlab.arturbosch.detekt' apply false
    id 'idea'
    id 'application'
    id 'com.jfrog.artifactory'
    id 'maven-publish'
    id 'org.ajoberstar.grgit' version '4.0.2'
}

wrapper {
    gradleVersion = '6.8.1'
    distributionType = Wrapper.DistributionType.BIN
}



if (!project.hasProperty('cordaArtifactoryUsername')) {
    ext.cordaArtifactoryUsername = System.getenv('CORDA_ARTIFACTORY_USERNAME')
}

if (! project.hasProperty('cordaArtifactoryPassword')) {
    ext.cordaArtifactoryPassword = System.getenv('CORDA_ARTIFACTORY_PASSWORD')
}

def flowworkerVersion
def releaseType = System.getenv("RELEASE_TYPE") ?: "SNAPSHOT"
def versionSuffix = System.getenv("VERSION_SUFFIX")
def artifactoryPublishRepo = System.getenv("CORDA_PUBLISH_REPOSITORY_KEY") ?: "corda-ent-maven-dev"

switch (releaseType) {
    case "SNAPSHOT":
        flowworkerVersion = project.hasProperty("snapshotLabel") ?
                "$flowworkerMajorMinorVersion.0-${project.property("snapshotLabel")}-SNAPSHOT" :
                "$flowworkerMajorMinorVersion.0-SNAPSHOT"
        break
    default:
        flowworkerVersion = "$flowworkerMajorMinorVersion.$versionSuffix"
}

artifactory {
    publish {
        contextUrl = artifactoryContextUrl
        repository {
            repoKey = artifactoryPublishRepo
            username = cordaArtifactoryUsername
            password = cordaArtifactoryPassword
            maven = true
        }
        defaults {
            publications(project.name)
            properties = ['release.type': releaseType, 'source.revision': grgit.head().id]
        }
    }
}


allprojects {
    apply plugin: 'org.jetbrains.kotlin.jvm'

    group 'net.corda'
    version flowworkerVersion

    tasks.withType(JavaCompile) {
        sourceCompatibility = VERSION_11
        targetCompatibility = VERSION_11
        options.encoding = 'UTF-8'
        options.compilerArgs += '-XDenableSunApiLintControl'
    }

    tasks.withType(KotlinCompile) {
        kotlinOptions {
            languageVersion = '1.4'
            apiVersion = '1.4'
            jvmTarget = VERSION_11
            javaParameters = true   // Useful for reflection.
            freeCompilerArgs += [
                    // Prevent Kotlin from warning about kotlin.* classes inside the OSGi bundle.
                    "-Xskip-runtime-version-check",
                    "-java-parameters"
            ]
        }
    }

    repositories {
        //repo for CI builds,
        maven {
            url = "$artifactoryContextUrl/corda-ent-maven-unstable"
            credentials {
                username = cordaArtifactoryUsername
                password = cordaArtifactoryPassword
            }
        }
        //repo for dev builds
        maven {
            url = "$artifactoryContextUrl/corda-ent-maven-dev"
            credentials {
                username = cordaArtifactoryUsername
                password = cordaArtifactoryPassword
            }
        }
        //needed for kotlin osgi bundles generated by us
        maven {
            url = "$artifactoryContextUrl/corda-dependencies"
        }
        mavenCentral()
        //TODO remove this when we can as its only needed for kotlinx-html-jvm
        jcenter()
    }

    sourceSets {
        integrationTest {
            kotlin {
                srcDirs = [ 'src/integration-test/kotlin' ]
            }
            resources {
                srcDirs = [ 'src/integration-test/resources' ]
            }
        }
    }

    configurations {
        integrationTestApi.extendsFrom testApi
        integrationTestCompileOnly.extendsFrom testCompileOnly
        integrationTestImplementation.extendsFrom testImplementation
    }

    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-osgi-bundle:$kotlinVersion"
        implementation "net.corda.kotlin:kotlin-stdlib-jdk8-osgi:$kotlinVersion"
        implementation "org.slf4j:slf4j-api:$slf4jVersion"

        // Test utilities
        testImplementation "org.junit.jupiter:junit-jupiter-api:$junit5Version"
        testImplementation "org.assertj:assertj-core:$assertjVersion"
        testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junit5Version"
    }

    tasks.named("test", Test).configure {
        useJUnitPlatform()
    }
}


subprojects {
    apply plugin: 'java-library'
    apply plugin: 'idea'
    apply plugin: 'kotlin-allopen'
    apply plugin: 'kotlin-noarg'
    apply plugin: DetektPlugin

    detekt {
        baseline = file("$projectDir/detekt-baseline.xml")
        config.setFrom(files("$rootDir/detekt-config.yml"))
        parallel = true
        reports {
            xml {
                enabled = true
                destination = file("$projectDir/build/detekt-report.xml")
            }
            html {
                enabled = false
            }
            txt {
                enabled = false
            }
        }
    }

    dependencies {
        detektPlugins "io.gitlab.arturbosch.detekt:detekt-formatting:$detektPluginVersion"
    }
}

