plugins {
    id 'org.jetbrains.kotlin.jvm'
}

description 'Worker Smoketests'

configurations {
    cpis {
        canBeConsumed = false
        transitive = false
    }
    cpiForFlowCacheTest {
        canBeConsumed = false
        transitive = false
    }
}

sourceSets {
    smokeTest {
        java {
            srcDirs += [ 'src/smokeTest/java' ]
        }
        kotlin {
            srcDirs += [ 'src/smokeTest/kotlin' ]
        }
        resources {
            srcDirs = [ 'src/smokeTest/resources' ]
        }
        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output
    }
}

kotlin {
    target {
        java
        compilations.smokeTest {
            associateWith compilations.main
            associateWith compilations.test

            configurations {
                smokeTestApi.extendsFrom testApi
                smokeTestImplementation.extendsFrom testImplementation
                smokeTestRuntimeOnly.extendsFrom testRuntimeOnly
            }
        }
    }
}

dependencies {
    // NO CORDA DEPENDENCIES!!
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core-jvm:1.4.2"

    // But building a cpb for use in a test is ok.
    cpis project(path: ':testing:cpbs:flow-worker-dev', configuration: 'cordaCPB')
    cpiForFlowCacheTest project(path: ':testing:cpbs:flow-worker-dev-for-cache-testing', configuration: 'cordaCPB')

    smokeTestImplementation "com.konghq:unirest-java:$unirestVersion"
    smokeTestImplementation "com.konghq:unirest-objectmapper-jackson:$unirestVersion"
    smokeTestImplementation "com.fasterxml.jackson.module:jackson-module-kotlin:$jacksonVersion"
    smokeTestImplementation project(':testing:test-utilities')

    smokeTestImplementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:$jacksonVersion"
    smokeTestImplementation "com.fasterxml.jackson.module:jackson-module-kotlin:$jacksonVersion"
    smokeTestImplementation "org.apache.commons:commons-text:$commonsTextVersion"
    smokeTestImplementation "org.assertj:assertj-core:$assertjVersion"
    smokeTestImplementation "org.junit.jupiter:junit-jupiter-api:$junit5Version"
    smokeTestImplementation project(':components:flow:flow-rpcops-service')
    smokeTestImplementation "org.eclipse.jetty.websocket:websocket-client:$jettyVersion"
    smokeTestImplementation "org.slf4j:slf4j-api:$slf4jVersion"
    smokeTestImplementation "r3.pqa.craft5:craft5-corda-client:$craft5Version"
    smokeTestImplementation "r3.pqa.craft5:craft5-common:$craft5Version"

    smokeTestRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junit5Version"
    smokeTestRuntimeOnly "org.slf4j:slf4j-simple:$slf4jVersion"
}

def smokeTestResources = tasks.named('processSmokeTestResources', ProcessResources) {
    from(configurations.cpis) {
        into 'META-INF'
        rename "(.+)-\\d+.+-package.cpb", "\$1.cpb"
    }

    // Put the flow-worker-dev-for-cache-testing cpb into a different folder
    def cpiForFlowCacheTest = configurations.cpiForFlowCacheTest
    def cacheTestingDir = "META-INF" + File.separator + "cache-invalidation-testing"
    from(cpiForFlowCacheTest) {
        into cacheTestingDir
        rename "(.+)-\\d+.+-package.cpb", "flow-worker-dev.cpb"
    }
}

tasks.register('smokeTest', Test) {
    description = "Runs smoke tests."
    group = "verification"

    testClassesDirs = project.sourceSets["smokeTest"].output.classesDirs
    classpath = project.sourceSets["smokeTest"].runtimeClasspath

    def combinedWorker = project.getProperties().getOrDefault("isCombinedWorker",false)
    systemProperty "rpcHost", project.getProperties().getOrDefault("rpcHost","https://localhost:8888/")
    systemProperty "cryptoWorkerHealthHttp",
            project.getProperties().getOrDefault("cryptoWorkerHealthHttp",combinedWorker ? null : "http://localhost:7001/")
    systemProperty "dbWorkerHealthHttp",
            project.getProperties().getOrDefault("dbWorkerHealthHttp",combinedWorker ? null : "http://localhost:7002/")
    systemProperty "flowWorkerHealthHttp",
            project.getProperties().getOrDefault("flowWorkerHealthHttp",combinedWorker ? null : "http://localhost:7003/")
    systemProperty "rpcWorkerHealthHttp",
            project.getProperties().getOrDefault("rpcWorkerHealthHttp",combinedWorker ? null : "http://localhost:7004/")
    systemProperty "combinedWorkerHealthHttp",
            project.getProperties().getOrDefault("combinedWorkerHealthHttp",combinedWorker ? "http://localhost:7000/" : null)
}


tasks.named('smokeTest', Test) {
    dependsOn smokeTestResources
}
