plugins {
    id 'com.r3.internal.gradle.plugins.r3Publish'
    id 'corda.common-library'
    id 'application'
}

description "P2P Testing tools - P2P layer deployment"

dependencies {
    implementation platform('org.jetbrains.kotlin:kotlin-bom')
    implementation project(':libs:p2p-test:stub-certificates-authority')
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    implementation "info.picocli:picocli:$picocliVersion"
    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:$jacksonVersion"
    implementation "com.konghq:unirest-java:$unirestVersion"
    implementation platform("net.corda:corda-api:$cordaApiVersion")
    implementation 'net.corda:corda-avro-schema'
    runtimeOnly "org.apache.logging.log4j:log4j-slf4j-impl:$log4jVersion"
}
application {
    mainClass = 'net.corda.p2p.deployment.GoKt'
}

def binaryProjects = [
        'p2p-test:network-map-creator',
        'p2p-test:p2p-configuration-publisher',
        'p2p-test:cryptoservice-key-creator',
        'kafka-setup'
]

binaryProjects.forEach {name->
    def shortName = name.split(':').last()
    def copyJars = tasks.register("copyJars$shortName", Copy) {
        def project = ":applications:tools:$name"
        def task = rootProject.findProject(project).tasks.getByName('appJar')
        dependsOn(task)
        from task.outputs.files.first()
        into "$buildDir/jars/"
        rename {
            "${shortName}.jar"
        }
    }
    processResources.dependsOn(copyJars)
}

sourceSets {
    main {
        resources {
            srcDirs "$buildDir/jars"
        }
    }
}
