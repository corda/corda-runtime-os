plugins {
    id 'corda.common-publishing'
    id 'corda.common-library'
}

description "P2P Testing tools - Fake CA"

dependencies {
    implementation platform('org.jetbrains.kotlin:kotlin-bom')
    implementation project(':libs:crypto:certificate-generation')
    implementation platform("net.corda:corda-api:$cordaApiVersion")
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    implementation "info.picocli:picocli:$picocliVersion"
    implementation 'net.corda:corda-crypto'
    implementation 'net.corda:corda-cipher-suite'
}

task appJar(type: Jar) {
    destinationDirectory = file("$buildDir/bin")
    archiveBaseName = "corda-${project.name}"
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    exclude 'META-INF/*.RSA', 'META-INF/*.SF','META-INF/*.DSA'
    manifest {
        attributes 'Main-Class': "net.corda.p2p.fake.ca.CaMainKt"
    }

    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    with jar
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId project.group
            artifact appJar
        }
    }
}
