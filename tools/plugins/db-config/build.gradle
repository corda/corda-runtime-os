plugins {
    id 'distribution'
    id 'org.jetbrains.kotlin.jvm'
    id 'org.jetbrains.kotlin.kapt'
}

group 'net.corda.cli.deployment'

dependencies {
    implementation 'net.corda.kotlin:kotlin-stdlib-jdk8-osgi'

    compileOnly "org.pf4j:pf4j:$pf4jVersion"
    compileOnly "net.corda.cli.host:api:$pluginHostVersion"
    kapt "org.pf4j:pf4j:$pf4jVersion"
    kapt "info.picocli:picocli-codegen:$picocliVersion"

    implementation platform("net.corda:corda-api:$cordaApiVersion")
    implementation 'net.corda:corda-db-schema'
    implementation 'javax.persistence:javax.persistence-api'
    implementation "org.liquibase:liquibase-core:$liquibaseVersion"

    testImplementation "org.mockito.kotlin:mockito-kotlin:$mockitoKotlinVersion"
    testImplementation "org.pf4j:pf4j:$pf4jVersion"
    testCompileOnly "net.corda.cli.host:api:$pluginHostVersion"
}

def pluginJar = tasks.named('jar', Jar) {
    manifest {
        attributes['Plugin-Class'] = pluginClass
        attributes['Plugin-Id'] = pluginId
        attributes['Plugin-Provider'] = pluginProvider
        attributes['Plugin-Description'] = pluginDescription
        attributes['Plugin-Version'] = version
    }
}

def plugin = tasks.register('plugin', Zip) {
    archiveBaseName = "plugin-$pluginId-$version"
    // first taking the classes generated by the jar task
    into('classes') {
        from zipTree(pluginJar.map { it.outputs.files.singleFile })
    }
    // and then we also need to include any libraries that are needed by the plugin
    dependsOn configurations.runtimeClasspath.buildDependencies
    into('lib') {
        from configurations.runtimeClasspath.filter { it.name.endsWith('jar')}
    }
}

tasks.named("installDist") {
    dependsOn plugin
    def homePath = System.properties['user.home']
    from plugin
    into "$homePath/.corda/cli/plugins"
}

artifacts {
    archives plugin
}
