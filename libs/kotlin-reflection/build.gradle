import aQute.bnd.gradle.Bundle
import aQute.bnd.gradle.Resolve
import aQute.bnd.gradle.TestOSGi
import com.gradle.enterprise.gradleplugin.test.ImportJUnitXmlReports
import static com.gradle.enterprise.gradleplugin.test.JUnitXmlDialect.GENERIC

plugins {
    id 'corda.common-publishing'
    id 'biz.aQute.bnd.builder'
    id 'corda.javadoc-generation'
}

ext {
    releasable = true
}

description "Bare bones Kotlin reflection within an OSGi framework."

configurations {
    bundle {
        canBeResolved = false
    }
    bundle.extendsFrom runtimeClasspath
}

dependencies {
    api 'net.corda.kotlin:kotlin-stdlib-jdk8-osgi'
    compileOnly 'org.osgi:osgi.annotation'
    compileOnly "org.jetbrains.kotlinx:kotlinx-metadata-jvm:$kotlinMetadataVersion"
    compileOnly "org.ow2.asm:asm:$asmVersion"
    compileOnly "co.paralleluniverse:quasar-osgi-annotations:$quasarVersion"
    implementation platform("net.corda:corda-api:$cordaApiVersion")

    testImplementation "org.assertj:assertj-core:$assertjVersion"
    testImplementation "org.junit.jupiter:junit-jupiter-params:$junit5Version"
    testRuntimeOnly "org.jetbrains.kotlinx:kotlinx-metadata-jvm:$kotlinMetadataVersion"
    testRuntimeOnly "org.ow2.asm:asm:$asmVersion"

    integrationTestImplementation project(':libs:kotlin-reflection:kotlin-reflection-test-example')
    integrationTestImplementation 'org.slf4j:slf4j-api'
    integrationTestImplementation "org.apache.felix:org.apache.felix.framework:$felixVersion"
    integrationTestImplementation "org.junit.jupiter:junit-jupiter-api:$junit5Version"
    integrationTestRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junit5Version"
    integrationTestRuntimeOnly "org.junit.platform:junit-platform-launcher:$junitPlatformVersion"
    integrationTestRuntimeOnly "org.slf4j:slf4j-simple:$slf4jVersion"
}

def jar = tasks.named('jar', Jar) {
    archiveBaseName = 'corda-kotlin-reflection'
    bundle {
        bnd """\
Bundle-Name: Corda Kotlin Reflection
Bundle-SymbolicName: \${project.group}.kotlin-reflection
Sealed: true
-includeresource: @kotlinx-metadata-jvm-${kotlinMetadataVersion}.jar
-conditionalpackage: org.objectweb.asm
"""
    }
}

def testingBundle = tasks.register('testingBundle', Bundle) {
    archiveClassifier = 'tests'
    from sourceSets.integrationTest.output
    sourceSet = sourceSets.integrationTest
    bundle {
        bnd '''\
Test-Cases: \${classes;HIERARCHY_INDIRECTLY_ANNOTATED;org.junit.platform.commons.annotation.Testable;CONCRETE}
Sealed: true
'''
    }
}

def resolve = tasks.register('resolve', Resolve) {
    dependsOn jar, testingBundle
    bundles = files(
        sourceSets.integrationTest.runtimeClasspath,
        configurations.archives.artifacts.files,
        testingBundle
    )
    bndrun = file('test.bndrun')
    outputBndrun = layout.buildDirectory.file('resolved-test.bndrun')
    doFirst {
        //  bnd attempts to use ~/ for caching if this is unavailable the build will fail.
        System.setProperty('bnd.home.dir', "$rootDir/bnd")
    }
}

// Gradle enterprise does not pick up OSGI tests by default as they they are of type TestOSGi rather than standard
def importOSGiJunitXml = tasks.register('importOSGiJUnitXml', ImportJUnitXmlReports) {
    dialect = GENERIC
    reports.from(fileTree("$testResultsDir/integrationTest").matching {
        include '**/TEST-*.xml'
    })
}

def testOSGi = tasks.register('testOSGi', TestOSGi) {
    resultsDirectory = file("$testResultsDir/integrationTest")
    bundles = files(
        sourceSets.integrationTest.runtimeClasspath,
        configurations.archives.artifacts.files,
        testingBundle
    )
    bndrun = resolve.flatMap { it.outputBndrun }
}

tasks.named('integrationTest') {
    dependsOn testOSGi
    enabled = false
}

artifacts {
    bundle jar
}
