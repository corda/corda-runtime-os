// This plugin is for providing the standard set of gradle used for all libraries
// wishing to implement the OSGi-aware integration test framework

import aQute.bnd.gradle.Bundle
import aQute.bnd.gradle.Resolve
import aQute.bnd.gradle.TestOSGi
import static org.gradle.jvm.toolchain.JavaLanguageVersion.of

import static com.gradle.enterprise.gradleplugin.test.JUnitXmlDialect.GENERIC
import com.gradle.enterprise.gradleplugin.test.ImportJUnitXmlReports

plugins {
    id 'biz.aQute.bnd.builder'
}

configurations {
    quasarBundles {
        canBeConsumed = false
        visible = false
    }
    integrationTestApi.extendsFrom testApi
    integrationTestCompileOnly.extendsFrom testCompileOnly
    integrationTestImplementation.extendsFrom testImplementation
}

ext {
    postgresHost = providers.systemProperty('postgresHost').orElse("")
    postgresPort = providers.systemProperty('postgresPort').orElse("")
    postgresDb = providers.systemProperty('postgresDb').orElse("")
    postgresUser = providers.systemProperty('postgresUser').orElse("")
    postgresPassword = providers.systemProperty('postgresPassword').orElse("")
}

dependencies {
    compileOnly 'org.osgi:osgi.annotation'
    compileOnly 'org.osgi:osgi.core'
    compileOnly 'org.osgi:org.osgi.service.component.annotations'

    // OSGi frameworks containing Quasar and Kryo must resolve
    // the version of ASM as specified in the OSGi metadata.
    quasarBundles("co.paralleluniverse:quasar-core-osgi:$quasarVersion") {
        exclude group: 'org.slf4j'
    }
    integrationTestRuntimeOnly("co.paralleluniverse:quasar-core-osgi:$quasarVersion:framework-extension") {
        transitive = false
    }
    integrationTestRuntimeOnly files(configurations.quasarBundles)

    integrationTestImplementation "org.junit.jupiter:junit-jupiter-api:$junit5Version"
    integrationTestImplementation "org.junit.jupiter:junit-jupiter-params:$junit5Version"
    integrationTestImplementation "org.osgi:org.osgi.test.junit5:$osgiTestJunit5Version"
    integrationTestRuntimeOnly "org.junit.platform:junit-platform-launcher:$junitPlatformVersion"
    integrationTestRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junit5Version"
    integrationTestRuntimeOnly "org.apache.felix:org.apache.felix.framework:$felixVersion"
    integrationTestRuntimeOnly "org.apache.felix:org.apache.felix.scr:$felixScrVersion"
    integrationTestRuntimeOnly "org.osgi:org.osgi.service.component:$osgiServiceComponentVersion"
    integrationTestRuntimeOnly "org.osgi:org.osgi.util.function:$osgiUtilFunctionVersion"
    integrationTestRuntimeOnly "org.osgi:org.osgi.util.promise:$osgiUtilPromiseVersion"
    integrationTestRuntimeOnly "org.slf4j:slf4j-simple:$slf4jVersion"
}

def testingBundle = tasks.register('testingBundle', Bundle) {
    archiveClassifier = 'tests'
    from sourceSets.integrationTest.output
    sourceSet = sourceSets.integrationTest

    bundle {
        bnd """\
Bundle-SymbolicName: \${task.archiveBaseName}-\${task.archiveClassifier}
Test-Cases: \${classes;HIERARCHY_INDIRECTLY_ANNOTATED;org.junit.platform.commons.annotation.Testable;CONCRETE}
"""
    }
}

def resolve = tasks.register('resolve', Resolve) {
    dependsOn jar, testingBundle
    bundles = files(sourceSets.integrationTest.runtimeClasspath, configurations.archives.artifacts.files)
    bndrun = file('test.bndrun')
    outputBndrun = layout.buildDirectory.file('resolved-test.bndrun')
    doFirst {
        //  bnd attempts to use ~/ for caching if this is unavailable the build will fail.
        System.setProperty("bnd.home.dir", "$rootDir/bnd/")
    }
}

def testOSGi = tasks.register('testOSGi', TestOSGi) {
    description = "Runs OSGi integration tests."
    group = "verification"
    javaLauncher = javaToolchains.launcherFor {
        languageVersion = of(11)
    }
    resultsDirectory = file("$testResultsDir/integrationTest")
    bundles = files(sourceSets.integrationTest.runtimeClasspath, configurations.archives.artifacts.files)
    bndrun = resolve.flatMap { it.outputBndrun }

    // These properties are for Quasar's framework extension, should it be present.
    properties.put '-runproperties.quasarExcludePackages', 'co.paralleluniverse.quasar.excludePackages="aQute.**,kotlin**,org.osgi.**,org.apache.felix.**,org.slf4j**,org.junit.**,org.assertj.**,org.opentest4j**"'
    properties.put '-runproperties.quasarSuspendableAnnotation', 'co.paralleluniverse.quasar.suspendableAnnotation=net.corda.v5.base.annotations.Suspendable'

    // Ensure we still get Gradle's project properties too.
    properties.put 'project', '__convention__'
}

tasks.named('check') {
    dependsOn testOSGi
}


//Gradle enterprise does not pick up OSGI tests by default as they they are of type TestOSGi rather than standard
// Test task this ensures test results are captured
def importTask = tasks.register("importOSGiJUnitXml", ImportJUnitXmlReports) {
    dialect = GENERIC
    reports.from(fileTree("$testResultsDir/integrationTest").matching {
        include '**/TEST-*.xml'
    })
}

tasks.named('integrationTest') {
    dependsOn testOSGi
    enabled = false
}

artifacts {
    archives testingBundle
}
