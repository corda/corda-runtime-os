plugins {
    id 'java-platform'
    id 'maven-publish'
    id 'com.jfrog.artifactory'
}

description = 'Declares the Corda API component versions.'

dependencies {
    constraints {
        api project(':application')
        api project(':base')
        api project(':cipher-suite')
        api project(':crypto')
        api project(':data:avro-schema')
        api project(':data:config-schema')
        api project(':data:topic-schema')
        api project(':data:db-schema')
        api project(':ledger')
        api project(':membership')
        api project(':membership-conversion')
        api project(':membership-identity')
        api project(':packaging')
        api project(':packaging:packaging-avro-converters')
        api project(':persistence')
        api project(':serialization')

        api "javax.persistence:javax.persistence-api:$javaxPersistenceApiVersion"
        api "net.corda.kotlin:kotlin-stdlib-jdk8-osgi:$kotlinVersion"
        api "net.corda.kotlin:kotlin-stdlib-jdk7-osgi:$kotlinVersion"
        api "org.osgi:osgi.annotation:$osgiVersion"
        api "org.osgi:osgi.core:$osgiVersion"
        api "org.osgi:org.osgi.service.component.annotations:$osgiScrAnnotationVersion"
        api "org.slf4j:slf4j-api:$slf4jVersion"
    }
}

publishing {
    publications {
        apiConfiguration(MavenPublication) {
            from components.javaPlatform

            pom {
                name = 'Corda API Platform'
                description = project.description

                url = vcsUrl - '.git'
                scm {
                    url = vcsUrl
                }

                licenses {
                    license {
                        name = licenseName
                        url = licenseUrl
                        distribution = 'repo'
                    }
                }

                developers {
                    developer {
                        id = 'R3'
                        name = 'R3'
                        email = 'dev@corda.net'
                    }
                }
            }
        }
    }
}

artifactoryPublish {
    publications('apiConfiguration')
}
