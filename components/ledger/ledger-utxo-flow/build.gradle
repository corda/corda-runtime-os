import aQute.bnd.gradle.Bundle

plugins {
    id 'corda.common-publishing'
    id 'corda.common-library'
    id 'corda.osgi-test-conventions'
}

description 'Corda UTXO ledger - flow implementation'

configurations {
    cpis {
        canBeConsumed = false
        transitive = false
    }
}

dependencies {
    compileOnly 'org.osgi:osgi.annotation'
    compileOnly 'org.osgi:org.osgi.service.component.annotations'

    implementation 'net.corda:corda-ledger-utxo'
    implementation 'net.corda:corda-topic-schema'
    implementation project(':libs:crypto:cipher-suite')
    implementation project(':libs:crypto:crypto-core')
    implementation project(':libs:flows:flow-api')
    implementation project(':libs:ledger:ledger-common-data')
    implementation project(':libs:ledger:ledger-utxo-data')
    implementation project(':libs:ledger:ledger-utxo-transaction-verifier')
    implementation project(':libs:flows:flow-api')
    implementation project(':components:ledger:ledger-common-flow-api')
    implementation project(':components:ledger:ledger-common-flow')
    implementation project(':libs:sandbox')
    implementation project(':libs:serialization:json-validator')
    implementation project(':libs:serialization:serialization-checkpoint-api')
    implementation project(':libs:serialization:serialization-internal')
    implementation project(':libs:virtual-node:sandbox-group-context')
    implementation project(':components:virtual-node:sandbox-notary')
    implementation project(':notary-plugins:notary-plugin-common')

    testImplementation "org.mockito.kotlin:mockito-kotlin:$mockitoKotlinVersion"
    testImplementation "org.jetbrains.kotlin:kotlin-test:$kotlinVersion"

    testImplementation project(':testing:test-serialization')
    testImplementation project(':testing:kryo-serialization-testkit')
    testImplementation project(':libs:serialization:serialization-internal')
    testImplementation project(':libs:serialization:serialization-amqp')
    testImplementation project(':libs:serialization:serialization-kryo')
    testImplementation project(':testing:ledger:ledger-utxo-base-test')

    integrationTestImplementation project(':testing:sandboxes')
    integrationTestImplementation project(':testing:sandboxes-testkit')

    testRuntimeOnly "org.slf4j:slf4j-simple:$slf4jVersion"

    integrationTestRuntimeOnly "org.apache.aries.spifly:org.apache.aries.spifly.dynamic.framework.extension:$ariesDynamicFrameworkExtensionVersion"
    integrationTestRuntimeOnly project(':libs:messaging:db-message-bus-impl')
    integrationTestRuntimeOnly project(':libs:messaging:messaging-impl')
    integrationTestRuntimeOnly project(':libs:lifecycle:lifecycle-impl')
    integrationTestRuntimeOnly project(':libs:flows:session-manager-impl')
    cpis project(path: ':testing:ledger:ledger-utxo-state-app', configuration: 'cordaCPB')
}

//  Copy the cpi builds declared in the cpis configuration into our resources so we find and load them
def integrationTestResources = tasks.named('processIntegrationTestResources', ProcessResources) {
    from(configurations.cpis) {
        into 'META-INF'
        rename "(.*)-\\Q${version}\\E-package.cpb", "\$1.cpb"
    }
}

tasks.named('testingBundle', Bundle) {
    dependsOn integrationTestResources
}
