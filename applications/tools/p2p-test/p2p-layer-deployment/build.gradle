plugins {
    id 'com.r3.internal.gradle.plugins.r3Publish'
    id 'corda.common-library'
    id 'application'
}

description "P2P Testing tools - P2P layer deployment"

dependencies {
    implementation platform('org.jetbrains.kotlin:kotlin-bom')
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    implementation "info.picocli:picocli:$picocliVersion"
    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:$jacksonVersion"

}
application {
    mainClass = 'net.corda.p2p.deployment.GoKt'
}

def binaryProjects = [
        'network-map-creator',
        'configuration-publisher',
        'cryptoservice-key-creator',
]

binaryProjects.forEach {name->
    def copyJars = tasks.register("copyJars$name", Copy) {
        def project = ":applications:tools:p2p-test:$name"
        def task = rootProject.findProject(project).tasks.getByName('appJar')
        dependsOn(task)
        from task.outputs.files.first()
        into "$buildDir/jars/"
        rename {
            "${name}.jar"
        }
    }
    processResources.dependsOn(copyJars)
}

sourceSets {
    main {
        resources {
            srcDirs "$buildDir/jars"
        }
    }
}
