plugins {
    id 'distribution'
    id 'org.jetbrains.kotlin.jvm'
    id 'org.jetbrains.kotlin.kapt'
    id 'corda.cli-plugin-packager'
    id 'corda.common-publishing'
}

ext {
    releasable = false
}

ext.cordaEnableFormatting = true

group 'net.corda.cli.deployment'

dependencies {

    implementation project(":libs:virtual-node:virtual-node-endpoints-maintenance")
    implementation project(":libs:virtual-node:cpi-upload-endpoints")

    implementation platform("net.corda:corda-api:$cordaApiVersion")
    implementation 'net.corda:corda-db-schema'

    implementation project(":tools:plugins:plugins-rest")
    compileOnly 'org.jetbrains.kotlin:kotlin-stdlib'
    compileOnly libs.pf4j
    compileOnly "net.corda.cli.host:api:$pluginHostVersion"

    kapt libs.pf4j
    kapt "info.picocli:picocli:$picocliVersion"

    implementation libs.liquibase
    constraints {
        implementation(libs.slf4j.v2.api)
        implementation(libs.snakeyaml)  {
            because "required until liquibase-core updates it's internal version of snakeYaml, currently using 1.33 which has CVE-2022-1471"
        }
    }

    // DO NOT DISTRIBUTE DRIVERS HERE WE ARE NOT LICENSED TO DISTRIBUTE
    // JDBC drivers are picked up in Corda 5 from a configured location, the CLI tool does not yet have this ability so
    // the Postgres drivers are packaged within the tool itself.
    runtimeOnly libs.postgresql.jdbc

    testImplementation libs.bundles.test
    testImplementation libs.pf4j
    testCompileOnly "net.corda.cli.host:api:$pluginHostVersion"
}

cliPlugin {
    cliPluginClass = 'net.corda.cli.plugins.vnode.VirtualNodeCliPlugin'
    cliPluginDescription = 'A CLI plug-in for operations on Virtual Node'
}

tasks.named("installDist") {
    dependsOn cliPlugin
    def homePath = System.properties['user.home']
    from cliPlugin
    into "$homePath/.corda/cli/plugins"
}

publishing {
    publications {
        maven(MavenPublication) {
            artifactId "${cliPlugin.cliPluginId.get()}-cli-plugin"
            groupId project.group
            artifact cliPluginTask
        }
    }
}
