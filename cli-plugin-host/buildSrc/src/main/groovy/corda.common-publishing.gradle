// plugin to cater for R3 vs Non R3 users building code base. R3 employees will leverage internal plugins non
// R3 users will use standard Maven publishing conventions as provided by the Maven-publish gradle plugin
if (System.getenv('CORDA_ARTIFACTORY_USERNAME') != null || project.hasProperty('cordaArtifactoryUsername')) {
    logger.info("Internal R3 user - resolving publication build dependencies from internal plugins")
    pluginManager.apply('com.r3.internal.gradle.plugins.r3Publish')
} else {
    logger.info("External user - using standard maven publishing")
    pluginManager.apply('maven-publish')
    pluginManager.withPlugin('java') {
        publishing {
            publications {
                register('maven', MavenPublication) {
                    afterEvaluate {
                        artifactId = tasks.named('jar', Jar).flatMap { it.archiveBaseName }.get()
                        groupId group.toString()
                        from components.findByName('kotlin') ?: components.java
                    }
                    pluginManager.withPlugin('net.corda.plugins.cordapp-cpk2') {
                        artifact tasks.named('cpk', Jar)
                    }
                    if (project.hasProperty("sourceJar")) {
                        maven.artifact(tasks.getByName("sourcesJar"))
                    }
                    if (project.hasProperty("javaDoc")) {
                        maven.artifact(tasks.getByName("javadocJar"))
                    }

                }
            }
        }
    }
}
