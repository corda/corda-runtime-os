// This plugin is for providing the standard set of gradle used for all libraries

plugins {
    id 'java-library'
    id 'biz.aQute.bnd.builder'
}

pluginManager.withPlugin('net.corda.plugins.cordapp-cpk') {
    throw new StopExecutionException('corda.common-library plugin is incompatible with building CPKs and CPBs')
}

configurations {
    testArtifacts {
        canBeResolved = false
    }
    testCompileOnly.extendsFrom compileOnly

    integrationTestApi.extendsFrom testApi
    integrationTestCompileOnly.extendsFrom testCompileOnly
    integrationTestImplementation.extendsFrom testImplementation

    all {
        resolutionStrategy {
            dependencySubstitution {
                substitute module('de.javakaffee:kryo-serializers') with project(':libs:serialization:kryo-serializers')
            }
        }
    }
}

dependencies {
    testImplementation "org.junit.jupiter:junit-jupiter-api:$junit5Version"
    testImplementation "org.junit.jupiter:junit-jupiter-params:$junit5Version"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junit5Version"

    integrationTestRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junit5Version"
}

tasks.named('jar', Jar) {
    archiveBaseName = "corda-${project.name}"
    bundle {
        bnd '''\
Bundle-Name: \${project.description}
Bundle-SymbolicName: \${project.group}.\${project.name}
'''
    }
}
