apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'corda.common-publishing'
apply plugin: 'corda.common-library'

description "Corda Universal Blob Inspector"

dependencies {
    // Use the Kotlin standard library.
    compileOnly 'org.jetbrains.kotlin:kotlin-stdlib'

    // AMQP support
    implementation "org.apache.qpid:proton-j:$protonjVersion"

    // Snappy (de)compression
    implementation "org.iq80.snappy:snappy:$snappyVersion"

    // Command line parsing
    implementation "info.picocli:picocli:$picocliVersion"

    // JSON
    implementation "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion"
}

ext {
    cordaEnableFormatting = true
}

tasks.named('jar', Jar) {
    destinationDirectory = layout.buildDirectory.dir('bin')
    archiveBaseName = "corda-universal-blob-inspector"
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    exclude 'META-INF/*.RSA', 'META-INF/*.SF','META-INF/*.DSA'
    manifest {
        attributes["Main-Class"] = 'net.corda.blobinspector.AppKt'
    }

    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}
