// plugin to cater for R3 vs Non R3 users building code base. R3 employees will leverage internal plugins non
// R3 users will use standard Maven publishing conventions as provided by the Maven-publish gradle plugin
if (System.getenv("CORDA_ARTIFACTORY_USERNAME") != null) {
    logger.lifecycle("Internal R3 user - resolving publication build dependencies from internal plugins")
    pluginManager.apply('com.r3.internal.gradle.plugins.r3Publish')
} else {
    logger.lifecycle("External user - using standard maven publishing")
    pluginManager.apply('maven-publish')
    pluginManager.withPlugin('java') {
        publishing {
            publications {
                register('maven', MavenPublication) {
                    artifactId "corda-${project.name}"
                    groupId group.toString()
                    from components.java
                    pluginManager.withPlugin('net.corda.plugins.cordapp-cpk') {
                        artifact tasks.named('cpk', Jar)
                    }
                    if (project.hasProperty("sourceJar")) {
                        maven.artifact(tasks.getByName("sourcesJar"))
                    }
                    if (project.hasProperty("javaDoc")) {
                        maven.artifact(tasks.getByName("javadocJar"))
                    }
                }
            }
        }
    }
}