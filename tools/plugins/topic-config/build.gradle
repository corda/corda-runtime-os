plugins {
    id 'distribution'
    id 'org.jetbrains.kotlin.jvm'
    id 'org.jetbrains.kotlin.kapt'
    id 'corda.cli-plugin-packager'
}

ext {
    releasable = false
}

group 'net.corda.cli.deployment'

dependencies {
    compileOnly 'org.jetbrains.kotlin:kotlin-stdlib'
    compileOnly libs.jackson.module.kotlin
    compileOnly libs.jackson.dataformat.yaml

    compileOnly libs.pf4j
    compileOnly "net.corda.cli.host:api:$pluginHostVersion"
    kapt libs.pf4j
    kapt "info.picocli:picocli:$picocliVersion"

    compileOnly platform("net.corda:corda-api:$cordaApiVersion")
    implementation "net.corda:corda-topic-schema:$cordaApiVersion"
    implementation "org.apache.servicemix.bundles:org.apache.servicemix.bundles.kafka-clients:$kafkaClientVersion"
    constraints {
        implementation(libs.slf4j.v2.api)
        implementation("org.xerial.snappy:snappy-java:$xerialSnappyVersion") {
            because 'Kafka Client uses an older version of Snappy library which is exposed to CVE-2023-34455. ' +
                    'This might be resolved in the future versions of Kafka Client.'
        }
    }

    testImplementation libs.bundles.test
    testImplementation 'org.jetbrains.kotlin:kotlin-stdlib'
    testImplementation libs.pf4j
    testImplementation "net.corda.cli.host:api:$pluginHostVersion"
    testImplementation libs.jackson.module.kotlin
    testImplementation libs.jackson.dataformat.yaml
    testImplementation "org.apache.servicemix.bundles:org.apache.servicemix.bundles.kafka-clients:$kafkaClientVersion"
}

cliPlugin {
    cliPluginClass = 'net.corda.cli.plugins.topicconfig.TopicPlugin'
    cliPluginDescription = 'Plugin for creating, and updating topics and ACLs.'
}

tasks.named("installDist") {
    dependsOn cliPlugin
    def homePath = System.properties['user.home']
    from cliPlugin
    into "$homePath/.corda/cli/plugins"
}
