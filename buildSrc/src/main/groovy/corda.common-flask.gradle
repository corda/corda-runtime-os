
plugins {
    id 'corda.common-app'
    id 'net.corda.plugins.flask'
    id 'net.corda.plugins.quasar-utils'
}

quasar {
    // Global configuration for our quasar-utils Gradle plugin.
    group = 'co.paralleluniverse'
    excludePackages = [
            'co.paralleluniverse**',
            'com.esotericsoftware.**',
            'jdk**',
            'junit**',
            'kotlin**',
            'net.rubygrapefruit.**',
            'org.gradle.**',
            'org.apache.**',
            'org.jacoco.**',
            'org.junit**',
            'org.slf4j**',
            'worker.org.gradle.**',
            'org.mockito.kotlin.*',
            'org.assertj**',
            'org.hamcrest**',
            'org.mockito**',
            'org.opentest4j**',
            'org.eclipse**'
    ]
}

def appFlask = project.tasks.register("appFlask", net.corda.gradle.flask.FlaskJarTask) {
    archiveBaseName = "corda-flask-" + project.name
    destinationDirectory = layout.buildDirectory.dir("bin")
    Provider<RegularFile> quasarAgent = layout.file(provider { configurations.quasarAgent.singleFile })
    includeLibraries(project.tasks.named("appJar").map { it.outputs }, quasarAgent)
    javaAgents {
        quasar {
            jar = quasarAgent
            args = project.quasar.options.map { it.substring(1) }.get()
        }
    }
    mainClassName = "net.corda.osgi.framework.OSGiFrameworkMain"
}

artifacts {
    archives appFlask
}
