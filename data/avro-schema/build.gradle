import com.github.davidmc24.gradle.plugin.avro.GenerateAvroJavaTask
import groovy.io.FileType
import groovy.text.SimpleTemplateEngine

plugins {
    id 'corda.publish'
    id 'corda.common-library'
    id "com.github.davidmc24.gradle.plugin.avro-base" version "$avroGradlePluginVersion"
}

dependencies {
    api "org.apache.avro:avro:$avroVersion"
    implementation "net.corda:corda-base:$cordaVersion"
}

description 'Data Model Definitions'

def generatedPackageInfoDir = "src/main/java"

sourceSets.main.java.srcDirs generatedPackageInfoDir

def generateAvro = tasks.register("generateAvro", GenerateAvroJavaTask) {
    group = "codeGeneration"
    source("src/main/resources/avro")
    outputDir = file(generatedPackageInfoDir)
}

avro {
    fieldVisibility = "PRIVATE"
}

// region Generate package-info.java
// The following code ensures that we have a `package-info.java` for every package that we
// generate through Avro

def generateOSGiPackageInfo = tasks.register("generateOSGiPackageInfo", Task) {
    group = "codeGeneration"
    def packages = [] as Set
    def classes = [] as Set
    file(generatedPackageInfoDir).eachFileRecurse(FileType.FILES) {
        if (!it.name.contains("package-info") && it.name.endsWith('.java')) {
            def packageName = ((it.text =~ "package (.+);")[0][1])
            packages << packageName
            def classCapture = it.text.normalize() =~ "@org.apache.avro.specific.AvroGenerated\npublic class (\\w+) extends org.apache.avro.specific.SpecificRecordBase"
            if (classCapture.any()) {
                classes << packageName + '.' + classCapture[0][1]
            }
        }
    }

    packages.each {
        def dir = "$generatedPackageInfoDir/${it.replaceAll(/\./, '/')}"
        def outputFile = file(dir + '/package-info.java').newWriter()
        outputFile << applyPackageInfoTemplate(it)
        outputFile.close()
    }

    sourceSets.main.resources {
        def generatedClassesDir = 'src/main/resources/net/corda/data'
        mkdir generatedClassesDir
        def outputFile = file(generatedClassesDir + '/generated-avro-message-classes.txt').newWriter()
        for (generatedClass in classes.sort()) {
            outputFile.println generatedClass.toString().denormalize()
        }
        outputFile.close()
    }

    dependsOn(generateAvro)
}

private static String applyPackageInfoTemplate(packageName) {
    def engine = new SimpleTemplateEngine()
    def templateText =
            """/**
 * Autogenerated by R3
 *
 * DO NOT EDIT DIRECTLY
 */
@Export
package $packageName;

import org.osgi.annotation.bundle.Export;
"""
    def templateParams = ['packageName': packageName]
    engine.createTemplate(templateText).make(templateParams).toString().denormalize()
}
// endregion

/**
 * Make this bundle a fragment of the Avro bundle to allow Avro to classload
 * all the classes from this bundle.
 */
tasks.named('jar', Jar) {
    archiveBaseName = project.name
    bnd """
Bundle-Name: $project.description
Bundle-SymbolicName: ${project.group}.${project.name}
Fragment-Host: avro
"""
}
