plugins {
    id 'distribution'
    id 'org.jetbrains.kotlin.jvm'
    id 'org.jetbrains.kotlin.kapt'
    id 'corda.cli-plugin-packager'
}

ext {
    releasable = false
}

group 'net.corda.cli.deployment'

dependencies {
    compileOnly 'org.jetbrains.kotlin:kotlin-stdlib'
    compileOnly "org.pf4j:pf4j:$pf4jVersion"
    compileOnly "net.corda.cli.host:api:$pluginHostVersion"
    kapt "org.pf4j:pf4j:$pf4jVersion"
    kapt "info.picocli:picocli:$picocliVersion"

    implementation platform("net.corda:corda-api:$cordaApiVersion")
    implementation 'net.corda:corda-db-schema'
    implementation 'javax.persistence:javax.persistence-api'
    implementation "org.liquibase:liquibase-core:$liquibaseVersion"
    constraints {
        implementation("org.yaml:snakeyaml:$snakeyamlVersion") {
            because "required until liquibase-core updates it's internal version of snakeYaml, currently using 1.33 which has CVE-2022-1471"
        }
        implementation('org.slf4j:slf4j-api') {
            version {
                strictly slf4jV2Version
            }
        }
    }

    // DO NOT DISTRIBUTE DRIVERS HERE WE ARE NOT LICENSED TO DISTRIBUTE
    // JDBC drivers are picked up in Corda 5 from a configured location, the CLI tool does not yet have this ability so
    // the Postgres drivers are packaged within the tool itself.
    runtimeOnly "org.postgresql:postgresql:$postgresDriverVersion"

    testImplementation 'org.jetbrains.kotlin:kotlin-stdlib'
    testImplementation libs.bundles.test
    testImplementation "org.pf4j:pf4j:$pf4jVersion"
    testCompileOnly "net.corda.cli.host:api:$pluginHostVersion"
}

cliPlugin {
    cliPluginClass = 'net.corda.cli.plugins.dbconfig.DatabaseBootstrapAndUpgrade'
    cliPluginDescription = 'Plugin for generating the initial database migrations.'
}

tasks.named("installDist") {
    dependsOn cliPlugin
    def homePath = System.properties['user.home']
    from cliPlugin
    into "$homePath/.corda/cli/plugins"
}