import static aQute.bnd.version.MavenVersion.parseMavenString

plugins {
    id 'biz.aQute.bnd.builder'
    id 'com.jfrog.artifactory'
    id 'maven-publish'
}

description 'Kotlin Coroutines'

configurations {
    bundle {
        canBeDeclared = false
        canBeResolved = false
    }
}

dependencies {
    implementation platform("org.jetbrains.kotlinx:kotlinx-coroutines-bom:$kotlinCoroutinesVersion")
    compileOnly 'org.jetbrains.kotlinx:kotlinx-coroutines-core-jvm'
}

def jar = tasks.named('jar', Jar) {
    archiveBaseName = 'corda-kotlin-coroutines'

    ext {
        bundleVersion = parseMavenString(kotlinCoroutinesVersion).OSGiVersion
    }

    bundle {
        bnd """\
Bundle-Name: \${project.description}
Bundle-SymbolicName: \${project.group}.kotlin-coroutines
Bundle-Version: \${task.bundleVersion}
Import-Package: \
    android.os;resolution:=optional,\
    sun.misc;resolution:=optional,\
    *
Export-Package: \
    kotlinx.coroutines.*
Multi-Release: true
Quasar-Ignore-Package: kotlinx.coroutines**
-fixupmessages: \
    "Export [^,]++,\\\\s++has (\\\\d++),\\\\s++private references "; restrict:=warning; is:=error,\
    "Classes found in the wrong directory"; restrict:=error; is:=warning
-includeresource: @kotlinx-coroutines-core-jvm-${kotlinCoroutinesVersion}.jar
"""
    }
}

tasks.named('sourcesJar', Jar) {
    enabled = false
}

artifacts {
    bundle jar
}

publishing {
    publications {
        coroutines(MavenPublication) {
            from components.java
        }
    }
}

artifactoryPublish {
    publications 'coroutines'
}
