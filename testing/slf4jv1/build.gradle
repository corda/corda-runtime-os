import static aQute.bnd.version.MavenVersion.parseMavenString

// This re-exports the SLF4J V1 API packages including org.slf4j.event and org.slf4j.spi which micrometer and kafka
// client depends on.
// This is only used in testOSGi gradle tasks. The real system exports SLF4J from the system bundle.

plugins {
    id 'java-library'
    id 'biz.aQute.bnd.builder'
    id 'com.jfrog.artifactory'
    id 'maven-publish'
}

description 'SLF4J V1 package exports'

dependencies {
    api libs.slf4j.api
}

tasks.named('jar', Jar) {
    archiveBaseName = 'corda-slf4jv1'

    ext {
        bundleVersion = parseMavenString(libs.slf4j.api.get().version).OSGiVersion
    }

    bundle {
        bnd """\
Bundle-Name: SLF4JV1
Bundle-SymbolicName: \${project.group}.testing.slf4jv1
Bundle-Version: \\${task.bundleVersion}
Export-Package: \\
     org.slf4j.event;uses:="org.slf4j,org.slf4j.helpers";version=2.0.13,\\
     org.slf4j.event;version=1.7.36,\\
     org.slf4j.helpers;uses:="org.slf4j,org.slf4j.event,org.slf4j.spi";version=2.0.13,\\
     org.slf4j.helpers;version=1.7.36,\\
     org.slf4j;uses:="org.slf4j.event,org.slf4j.helpers";version=2.0.13,\\
     org.slf4j;version=1.7.36
"""
    }
}
