plugins {
    id 'org.jetbrains.kotlin.jvm'
    id 'corda.quasar-app'
}

description 'VNode Sandbox Example'

osgiRun {
    frameworkProperties.putAll([
        'co.paralleluniverse.quasar.service': 'true',
        'org.osgi.framework.security': 'osgi'
    ])
}

quasar {
    cacheLocations = [ 'FLOW/*' ]
    verbose = false
}

configurations {
    cpis {
        canBeConsumed = false
    }
}

dependencies {
    compileOnly 'org.osgi:osgi.annotation'
    compileOnly 'org.osgi:osgi.core'
    compileOnly "org.osgi:org.osgi.service.component:$osgiServiceComponentVersion"
    compileOnly 'org.osgi:org.osgi.service.component.annotations'
    compileOnly "co.paralleluniverse:quasar-core-osgi:$quasarVersion:framework-extension"

    implementation platform("net.corda:corda-api:$cordaApiVersion")
    implementation "net.corda:corda-config-schema"
    implementation 'net.corda:corda-topic-schema'
    implementation 'net.corda:corda-serialization'
    implementation project(":libs:packaging:packaging")
    implementation 'net.corda:corda-base'
    implementation 'net.corda:corda-application'
    implementation 'org.jetbrains.kotlin:kotlin-osgi-bundle'
    implementation 'org.slf4j:slf4j-api'

    implementation project(':testing:sandboxes')
    implementation project(":libs:configuration:configuration-core")
    implementation project(':libs:messaging:messaging')
    implementation project(':libs:virtual-node:sandbox-group-context')
    implementation project(':libs:flows:utils')
    implementation project(':components:flow:flow-service')
    implementation project(":components:membership:membership-group-read")
    implementation project(':components:virtual-node:sandbox-group-context-service')
    implementation project(":libs:crypto:crypto-core")

    runtimeOnly "org.apache.felix:org.apache.felix.scr:$felixScrVersion"
    runtimeOnly "org.osgi:org.osgi.service.component:$osgiServiceComponentVersion"
    runtimeOnly "org.apache.aries.spifly:org.apache.aries.spifly.dynamic.framework.extension:$ariesDynamicFrameworkExtensionVersion"
    runtimeOnly "org.osgi:org.osgi.util.function:$osgiUtilFunctionVersion"
    runtimeOnly "org.osgi:org.osgi.util.promise:$osgiUtilPromiseVersion"

    runtimeOnly project(":components:membership:membership-group-read-impl")
    runtimeOnly project(':libs:application:application-impl')
    runtimeOnly project(':libs:crypto:cipher-suite-impl')
    runtimeOnly project(':libs:flows:session-manager-impl')
    runtimeOnly project(':libs:lifecycle:lifecycle-impl')
    runtimeOnly project(':libs:messaging:messaging-impl')
    runtimeOnly project(':libs:messaging:db-message-bus-impl')
    runtimeOnly project(':libs:serialization:serialization-checkpoint-api')
    runtimeOnly project(':libs:serialization:serialization-kryo')

    // Add OSGi security support.
    implementation project(':components:security-manager')
    runtimeOnly("org.apache.felix:org.apache.felix.framework.security:$felixSecurityVersion") {
        exclude group: 'org.apache.felix'
        exclude group: 'org.osgi'
    }

    cpis project(path: 'example-cpi', configuration: 'cordaCPB')
}

def resources = tasks.named('processResources', ProcessResources) {
    from(configurations.cpis) {
        rename 'example-cpi-(.*)', 'example-cpi-package.cpb'
        into 'META-INF'
        fileMode = 0444
    }
}

tasks.named('jar', Jar) {
    dependsOn resources
}
