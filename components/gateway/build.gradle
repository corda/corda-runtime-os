import java.nio.charset.Charset

plugins {
    id 'corda.common-library'
    id 'com.r3.internal.gradle.plugins.r3Publish'
}

description 'P2P gateway component'

dependencies {
    implementation platform("net.corda:corda-api:$cordaApiVersion")
    implementation "net.corda:corda-base"
    implementation "net.corda:avro-schema"
    implementation "net.corda:topic-schema"
    implementation project(":libs:p2p-crypto")
    implementation project(":libs:messaging:messaging")
    implementation project(":libs:lifecycle")

    implementation "com.typesafe:config:$typeSafeConfigVersion"
    implementation "io.reactivex:rxjava:$rxjavaVersion"
    implementation "io.netty:netty-transport:$nettyVersion"
    implementation "io.netty:netty-codec-http:$nettyVersion"
    implementation "org.bouncycastle:bcprov-jdk15on:$bouncyCastleVersion"
    implementation "org.bouncycastle:bcpkix-jdk15on:$bouncyCastleVersion"
    implementation "commons-validator:commons-validator:$commonsVersion"

    // Log4J: logging framework (with SLF4J bindings) - TEMPORARY UNTIL I FIGURE OUT LOGGING IN THIS PROJECT
    runtimeOnly "org.apache.logging.log4j:log4j-slf4j-impl:$log4jVersion"
    implementation "org.apache.logging.log4j:log4j-core:$log4jVersion"
    implementation "org.slf4j:jul-to-slf4j:$slf4jVersion"
    implementation "org.slf4j:jcl-over-slf4j:$slf4jVersion"
    // For async logging
    implementation "com.lmax:disruptor:$disruptorVersion"

    testImplementation "org.mockito:mockito-core:$mockitoVersion"
    testImplementation("org.mockito.kotlin:mockito-kotlin:$mockitoKotlinVersion") {
        exclude group: 'mockito-core'
    }

    testImplementation project(":libs:messaging:inmemory-messaging-impl")
}

sourceSets {
    integrationTest {
        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output
    }
}

tasks.register('integrationTest', Test) {
    description = "Runs integration tests."
    final String hostsFilePath = new File(project.sourceSets["integrationTest"].output.resourcesDir.absolutePath, "hosts").absolutePath
    jvmArgs "-Djdk.net.hosts.file=$hostsFilePath"
    testClassesDirs = project.sourceSets["integrationTest"].output.classesDirs
    classpath = project.sourceSets["integrationTest"].runtimeClasspath
}