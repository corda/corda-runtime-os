import static org.gradle.api.JavaVersion.VERSION_11
import io.gitlab.arturbosch.detekt.DetektPlugin

buildscript {
    repositories {
        maven {
            url "$artifactoryContextUrl/corda-releases"
        }
    }
}

plugins {
    id 'org.jetbrains.kotlin.jvm'
    id 'org.jetbrains.kotlin.plugin.allopen' apply false
    id 'org.jetbrains.kotlin.plugin.noarg' apply false
    id 'io.gitlab.arturbosch.detekt' apply false
    id 'idea'
    id 'application'
    id 'com.r3.internal.gradle.plugins.r3Publish' apply false
    id 'maven-publish'
    id 'jacoco'
}

wrapper {
    gradleVersion = '6.8.3'
    distributionType = Wrapper.DistributionType.BIN
    distributionUrl="https://gradleproxy:gradleproxy@software.r3.com/artifactory/gradle-proxy/gradle-$gradleVersion-bin.zip"
}

def javaVersion = VERSION_11

allprojects {
    if (!project.hasProperty('cordaArtifactoryUsername')) {
        ext.cordaArtifactoryUsername = System.getenv('CORDA_ARTIFACTORY_USERNAME')
    }

    if (!project.hasProperty('cordaArtifactoryPassword')) {
        ext.cordaArtifactoryPassword = System.getenv('CORDA_ARTIFACTORY_PASSWORD')
    }

    apply plugin: 'org.jetbrains.kotlin.jvm'

    tasks.withType(JavaCompile) {
        sourceCompatibility = javaVersion
        targetCompatibility = javaVersion
        options.encoding = 'UTF-8'
        options.compilerArgs += '-XDenableSunApiLintControl'
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile) {
        kotlinOptions {
            allWarningsAsErrors = true
            languageVersion = '1.4'
            apiVersion = '1.4'
            jvmTarget = javaVersion
            javaParameters = true   // Useful for reflection.
            freeCompilerArgs += [
                    // Prevent Kotlin from warning about kotlin.* classes inside the OSGi bundle.
                    "-Xskip-runtime-version-check",
                    "-java-parameters",
                    "-Xjvm-default=all"
            ]
        }
    }

    repositories {
        // TODO: you really need to use the cache, otherwise no complaining when upstream repos throttle us!
        // It would make sense to follow the template!

        // Listed the mavenLocal repository first in case people are doing a local publication of an upstream
        //  module (e.g in corda-api repo).
        //  This would ensure that the local copy is picked up if it is available.
        mavenLocal()

        //repo for CI builds,
        maven {
            url = "$artifactoryContextUrl/corda-ent-maven-unstable"
            credentials {
                username = cordaArtifactoryUsername
                password = cordaArtifactoryPassword
            }
        }
        //repo for dev builds
        maven {
            url = "$artifactoryContextUrl/corda-ent-maven-dev"
            credentials {
                username = cordaArtifactoryUsername
                password = cordaArtifactoryPassword
            }
        }
        //needed for kotlin osgi bundles generated by us
        maven {
            url = "$artifactoryContextUrl/corda-dependencies"
        }
        //needed for C5 binaries
        maven {
            url = "$artifactoryContextUrl/corda-os-maven-unstable"
            credentials {
                username = cordaArtifactoryUsername
                password = cordaArtifactoryPassword
            }
        }
        mavenCentral()
        //TODO remove this when we can as its only needed for kotlinx-html-jvm
        jcenter()
    }

    sourceSets {
        integrationTest {
            kotlin {
                srcDirs = [ 'src/integration-test/kotlin' ]
            }
            resources {
                srcDirs = [ 'src/integration-test/resources' ]
            }
        }
    }

    configurations {
        integrationTestApi.extendsFrom testApi
        integrationTestCompileOnly.extendsFrom testCompileOnly
        integrationTestImplementation.extendsFrom testImplementation
    }

    dependencies {
        // Exported as OSGi 'system packages extra' at runtime by the `osgi-framework-bootstrap` module.
        implementation "net.corda.kotlin:kotlin-stdlib-jdk8-osgi:$kotlinVersion"
        implementation "org.apache.logging.log4j:log4j-slf4j-impl:$log4jVersion"
        implementation "org.jetbrains.kotlin:kotlin-osgi-bundle:$kotlinVersion"

        // Test utilities
        testImplementation "org.assertj:assertj-core:$assertjVersion"
        testImplementation "org.junit.jupiter:junit-jupiter-api:$junit5Version"
        testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junit5Version"
    }

    tasks.named("test", Test).configure {
        useJUnitPlatform()
    }
}

logger.quiet("********************** CORDA FLOW WORKER BUILD **********************")
if (JavaVersion.current() != javaVersion) {
    throw new GradleException("The java version used ${JavaVersion.current()} is not the expected version ${javaVersion}.")
}
logger.quiet("SDK version: {}", JavaVersion.current())
logger.quiet("JAVA HOME {}", System.getProperty("java.home"))

def cordaVersion = "$cordaProductVersion.$cordaRuntimeRevision"
if (System.getenv("RELEASE_VERSION")?.trim()) {
    version = System.getenv("RELEASE_VERSION")
} else {
    def versionSuffix = '-SNAPSHOT'
    if(project.hasProperty('overridePublishVersionSuffix')) {
        versionSuffix = project.property('overridePublishVersionSuffix')
    } else if (System.getenv('VERSION_SUFFIX')) {
        versionSuffix = System.getenv('VERSION_SUFFIX')
    }
    version = "$cordaVersion$versionSuffix"
}
logger.quiet("Corda runtime OS release version: {}", version)
logger.quiet("Corda API dependency version spec: {}", cordaApiVersion)
logger.quiet("Old Corda dependencies version spec: {}", cordaVersion)
//logger.quiet("Release Type: {}", releaseType)

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'idea'
    apply plugin: 'kotlin-allopen'
    apply plugin: 'kotlin-noarg'
    apply plugin: DetektPlugin
    apply plugin: 'jacoco'

    version rootProject.version
    group 'net.corda'

    detekt {
        baseline = file("$projectDir/detekt-baseline.xml")
        config.setFrom(files("$rootDir/detekt-config.yml"))
        parallel = true
        reports {
            xml {
                enabled = true
                destination = file("$projectDir/build/detekt-report.xml")
            }
            html {
                enabled = false
            }
            txt {
                enabled = false
            }
        }
    }

    dependencies {
        detektPlugins "io.gitlab.arturbosch.detekt:detekt-formatting:$detektPluginVersion"
    }

    task allDependencyInsight(type: DependencyInsightReportTask) {}
}

