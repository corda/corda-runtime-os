// This plugin is for providing the standard set of gradle used for all modules
// wishing to publish to the R3 artifactory

plugins {
    id 'com.jfrog.artifactory'
    id 'maven-publish'
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId project.group
            versionMapping {
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            afterEvaluate {
                version tasks.jar.archiveVersion.get()

                if (project.hasProperty('artifact')) {
                    artifact project.ext.artifact
                    artifactId project.ext.artifact.archiveBaseName.get()
                } else {
                    artifactId tasks.jar.archiveBaseName.get()
                }
            }
            from components.kotlin
        }
    }
}

afterEvaluate {
    artifactoryPublish.dependsOn(jar)
}

artifactoryPublish {
    publications('maven')
}
