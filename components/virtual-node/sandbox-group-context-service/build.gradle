import aQute.bnd.gradle.Bundle

plugins {
    id 'com.r3.internal.gradle.plugins.r3Publish'
    id 'corda.osgi-test-conventions'
    id 'corda.common-library'
}

description "Sandbox Context Group Service"

configurations {
    cpis {
        canBeConsumed = false
        transitive = false
    }
}

dependencies {
    compileOnly 'org.osgi:osgi.core'
    compileOnly 'org.osgi:osgi.annotation'
    compileOnly "org.osgi:org.osgi.service.component.annotations"
    compileOnly "org.osgi:org.osgi.service.component:$osgiServiceComponentVersion"

    implementation platform("net.corda:corda-api:$cordaApiVersion")
    implementation "net.corda:corda-packaging"
    implementation "net.corda:corda-base"
    implementation "net.corda:corda-avro-schema"
    implementation "net.corda:corda-topic-schema"
    implementation "net.corda:corda-config-schema"

    implementation "net.corda:corda-packaging-avro-converters"

    implementation project(":libs:configuration:configuration-core")
    implementation project(":libs:crypto:crypto-core")
    implementation project(":libs:lifecycle:lifecycle")
    implementation project(":libs:messaging:messaging")

    implementation project(':libs:virtual-node:packaging-core')
    implementation project(':libs:virtual-node:sandbox-group-context')
    implementation project(':libs:sandbox')

    implementation project(":components:virtual-node:virtual-node-common")
    implementation project(":components:virtual-node:cpk-read-service")
    implementation project(":components:configuration:configuration-read-service")

    runtimeOnly project(':libs:sandbox-hooks')

    testImplementation project(":libs:lifecycle:lifecycle-impl")
    testImplementation project(":libs:lifecycle:registry")
    testImplementation 'net.corda:corda-serialization'

    testImplementation "org.assertj:assertj-core:$assertjVersion"
    testImplementation "org.mockito:mockito-core:$mockitoVersion"
    testImplementation "org.mockito.kotlin:mockito-kotlin:$mockitoKotlinVersion"

    testCompileOnly 'org.osgi:osgi.core'
    testRuntimeOnly "org.apache.felix:org.apache.felix.framework:$felixVersion"
    testRuntimeOnly "org.apache.felix:org.apache.felix.scr:$felixScrVersion"
    testRuntimeOnly "org.osgi:org.osgi.util.promise:$osgiUtilPromiseVersion"

    integrationTestImplementation 'net.corda:corda-application'
    integrationTestImplementation project(':testing:sandboxes')
    integrationTestRuntimeOnly project(':libs:crypto:crypto-impl')
    integrationTestRuntimeOnly project(':libs:lifecycle:lifecycle-impl')
    integrationTestRuntimeOnly project(':libs:messaging:inmemory-messaging-impl')

    // Gather locations of CPIs to add into `resources`
    cpis project(path: ':testing:cpbs:crypto-custom-digest-one-consumer', configuration: 'cordaCPB')
    cpis project(path: ':testing:cpbs:crypto-custom-digest-two-consumer', configuration: 'cordaCPB')
    cpis project(path: ':testing:cpbs:corda-services', configuration: 'cordaCPB')
}

def integrationTestResources = tasks.named('processIntegrationTestResources', ProcessResources) {
    from(configurations.cpis) {
        into 'META-INF'
        rename "(.*)-\\Q${version}\\E-package.cpb", "\$1.cpb"
    }
}

tasks.named('testingBundle', Bundle) {
    dependsOn integrationTestResources
}
